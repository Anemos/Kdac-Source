$PBExportHeader$f_pisc_retrieve_dddw_code.srf
$PBExportComments$dddw을 조회하는 함수 - Code 마스터
global type f_pisc_retrieve_dddw_code from function_object
end type

forward prototypes
global subroutine f_pisc_retrieve_dddw_code (datawindow fdw_1, string fs_areacode, string fs_divisioncode, string fs_codegroup, string fs_codeid, boolean fb_allflag, ref string rs_codegroup, ref string rs_codeid, ref string rs_codegroupname, ref string rs_codename)
end prototypes

global subroutine f_pisc_retrieve_dddw_code (datawindow fdw_1, string fs_areacode, string fs_divisioncode, string fs_codegroup, string fs_codeid, boolean fb_allflag, ref string rs_codegroup, ref string rs_codeid, ref string rs_codegroupname, ref string rs_codename);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		:	f_pisc_retrieve_dddw_code
//	Access		:	public
//	Arguments	:	DataWindow		fdw_1						조회하고자 하는 DDDW Object
//						string			fs_areacode				조회하고자 하는 지역
//						string			fs_divisioncode		조회하고자 하는 공장 코드
//						string			fs_codegroup			조회하고자 하는 코드 그룹
//						string			fs_codeid				조회하고자 하는 코드 (일반적으로 '%' 을 사용하도록)
//						boolean			fb_allflag				조회된 코드 정보가 2개 이상의 Record 일 경우
//																		True : '전체' 항목 삽입 (코드는 '%', 코드명은 '전체')
//																		False : '전체' 항목 미 삽입
//						string			rs_codegroup			선택된 코드 그룹 (reference)
//						string			rs_codeid				선택된 코드 명 (reference)
//						string			rs_codegroupname		선택된 코드 그룹 명(reference)
//						string			rs_codename				선택된 코드 명 (reference)
//	Returns		: none
//	Description	: 코드 마스터을 선택하기 위한 DDDW 을 조회하기 위하여
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Kim Jin-Su
// Coded Date	: 2002.09.04
///////////////////////////////////////////////////////////////////////////////////////////////////////////
Long ll_rowcount
Datawindowchild ldwc_1

//fdw_1.InsertRow(0)

If fdw_1.GetChild('DDDWCode', ldwc_1) = 1 Then
	ldwc_1.SetTransObject(sqlpis)
	ldwc_1.Retrieve(fs_areacode, fs_divisioncode, fs_codegroup, fs_codeid)
	ll_rowcount = ldwc_1.RowCount()
	If ll_rowcount < 1 Then
		fdw_1.ReSet()
		fdw_1.InsertRow(0)
		rs_codegroup		= ''
		rs_codeid			= ''
		rs_codegroupname	= ''
		rs_codename			= ''
	ElseIf ll_rowcount	= 1 Then
		rs_codegroup		= Trim(ldwc_1.GetItemString(1, 'CodeGroup'))
		rs_codeid			= Trim(ldwc_1.GetItemString(1, 'CodeID'))
		rs_codegroupname	= Trim(ldwc_1.GetItemString(1, 'CodeGroupName'))
		rs_codename			= Trim(ldwc_1.GetItemString(1, 'CodeName'))
		fdw_1.Setitem(1, 'DDDWCode', rs_codeid)
		f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'CodeRef03', 20)
	ElseIf ll_rowcount > 1 Then
		If fb_allflag = False Then
			rs_codegroup		= Trim(ldwc_1.GetItemString(1, 'CodeGroup'))
			rs_codeid			= Trim(ldwc_1.GetItemString(1, 'CodeID'))
			rs_codegroupname	= Trim(ldwc_1.GetItemString(1, 'CodeGroupName'))
			rs_codename			= Trim(ldwc_1.GetItemString(1, 'CodeName'))
			fdw_1.Setitem(1, 'DDDWCode', rs_codeid)
			f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'CodeRef03', 20)
		Else
			fdw_1.GetChild('DDDWCode', ldwc_1)
			ldwc_1.InsertRow(1)
			ldwc_1.Setitem(1, 'CodeID', 'ALL')
			ldwc_1.Setitem(1, 'CodeName', '전체')
			ldwc_1.Setitem(1, 'DisplayName', '전체')
			rs_codegroup		= fs_codegroup
			rs_codeid			= '%'
			rs_codegroupname	= Trim(ldwc_1.GetItemString(2, 'CodeGroupName'))
			rs_codename			= 'ALL'
			fdw_1.Setitem(1, 'DDDWCode', 'ALL')
			f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'CodeRef03', 20)
		End If
	End If
End If
end subroutine

