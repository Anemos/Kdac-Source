$PBExportHeader$f_rqno_pur.srf
$PBExportComments$구매요구번호 부여(부서코드)
global type f_rqno_pur from function_object
end type

forward prototypes
global function string f_rqno_pur (string ag_deptcd)
end prototypes

global function string f_rqno_pur (string ag_deptcd);/////////////////////////////////////////////////////////////////////////
//	구매요구번호 
//			argument : 부서코드
//    	체계 : XXXX - XXX - XXXX
//              부서 - 년월 - 시리얼
/////////////////////////////////////////////////////////////////////////

string ls_xyear //년월 비교
long  ln_ctlno //일련번호
string ls_ckey //rqno + 부서코드
string ls_rqno //결과 

ls_ckey = trim('RQNO' + ag_deptcd)

// 년월과 일련번호 받아오기
SELECT "PBINV"."INV403"."XYEAR",   
		"PBINV"."INV403"."SRNO"  
INTO :ls_xyear,   
	 :ln_ctlno  
FROM "PBINV"."INV403"
WHERE "PBINV"."INV403"."COMLTD" = '01'  
  AND "PBINV"."INV403"."CKEY" = :ls_ckey;
//  
If sqlca.sqlcode = 0 Then	
  IF ls_xyear = left(g_s_date,6) Then   // 년월 같으면 일련번호 증가
	  ln_ctlno = ln_ctlno + 1		  
  Else 
	  ls_xyear = left(g_s_date,6)		//다른면 년월 부여 , 일련번호 부여
	  ln_ctlno = 1
  End IF	 
  // Control DB
  UPDATE "PBINV"."INV403"
	 SET "SRNO" = :ln_ctlno,   
		  "XYEAR" = :ls_xyear  
  WHERE "PBINV"."INV403"."COMLTD" = '01'
    AND "PBINV"."INV403"."CKEY" = :ls_ckey ;
	   
  IF sqlca.sqlcode <> 0 then
	  MessageBox("Inv403 upt SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
	  Rollback using sqlca;		  
	  Return "er"
  End IF
 
  ls_rqno = ag_deptcd + mid(g_s_date,4,3) + string(ln_ctlno,"0000")//구매요구번호
  
ElseIf sqlca.sqlcode = 100 Then //DATA NOT FOUND
	ls_rqno =  ag_deptcd  + mid(g_s_date,4,3) + string(1,"0000")//구매요구번호
	
	ls_xyear  = left(g_s_date,6)
	ln_ctlno  = 1
	
	// Control DB
	INSERT INTO "PBINV"."INV403"  
         ( "COMLTD",  "CKEY",   "SRNO",    "JSCD",   "FROMDT",   "TODT",   "XYEAR",     "XYEAR1",   
           "RPFL",    "ISFL",   "RMRT",    "EXDTC",  "VANCD",    "PGMID",  "XCOMMENT",  "EXTD",   
           "INPTID",      "INPTDT",      "UPDTID",     "UPDTDT",      "IPADDR",         "MACADDR" )  
   VALUES ( '01',    :ls_ckey ,  :ln_ctlno,   ' ',      ' ',        ' ',     :ls_xyear,    ' ',   
           ' ',     ' ',       ' ',        ' ',      ' ',        ' ',     ' ',          ' ',   
           :g_s_empno,   :g_s_datetime,  :g_s_empno,   :g_s_datetime,   :g_s_ipaddr,    :g_s_macaddr )  ;

	IF sqlca.sqlcode <> 0 then
		MessageBox("Inv403 ins SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
	   Rollback using sqlca;		
		Return "er"
	End IF
	
Else
	MessageBox("SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
	return 'er'	
End IF

Return ls_rqno
end function

