$PBExportHeader$f_dw_setcolumn.srf
$PBExportComments$Extend Column Set  Global Function
global type f_dw_setcolumn from function_object
end type

forward prototypes
global subroutine f_dw_setcolumn (datawindow ag_dw)
end prototypes

global subroutine f_dw_setcolumn (datawindow ag_dw);////////////////////////////////////////////////////////////
// Table의 Extend Column Set...
// 
// (INPTID/INPTDT/UPDTID/UPDTDT/IPADDR/MACADDR)
////////////////////////////////////////////////////////////

String ls_ColCnt
Long	 ll_RowCnt

ls_ColCnt = ag_dw.Object.DataWindow.Column.Count
ll_RowCnt = ag_dw.RowCount()


String ls_ColName
Long	 ll_Row = 0, ll_i

Do While ll_Row <= ll_RowCnt
	ll_Row = ag_dw.GetNextModified(ll_Row, Primary!)
	If ll_Row = 0 Then Exit
	
	If ag_dw.GetItemStatus(ll_Row, 0, Primary!) = NewModified! Then  // 등록시 Extend Column 설정... 
		For ll_i = 1 To Dec(ls_ColCnt)
			ls_ColName = ag_dw.Describe('#' + String(ll_i) + '.Name')
			
			Choose Case Upper(Right(ls_ColName, 6))
				Case 'INPTID', 'UPDTID'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_empno)
				Case 'INPTDT'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_date)
				Case 'UPDTDT'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_datetime)
				Case 'IPADDR'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_ipaddr)					
				Case 'ACADDR'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_macaddr)
			End Choose
			
		Next
	ElseIf ag_dw.GetItemStatus(ll_Row, 0, Primary!) = DataModified! Then  // 수정시 Extend Column 설정...
		For ll_i = 1 To Dec(ls_ColCnt)
			ls_ColName = ag_dw.Describe('#' + String(ll_i) + '.Name')
			
			Choose Case Upper(Right(ls_ColName, 6))
				Case 'UPDTID'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_empno)
				Case 'UPDTDT'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_datetime)
				Case 'IPADDR'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_ipaddr)					
				Case 'ACADDR'
					ag_dw.SetItem(ll_Row, ls_ColName, g_s_macaddr)
			End Choose			
			
		Next
	End If
Loop
end subroutine

