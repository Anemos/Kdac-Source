$PBExportHeader$f_populate_ddlb_from_dac001.srf
$PBExportComments$Populate a DropDownListBox with data from a table in the database.
global type f_populate_ddlb_from_dac001 from function_object
end type

forward prototypes
global function integer f_populate_ddlb_from_dac001 (dropdownlistbox ddlb_parm)
end prototypes

global function integer f_populate_ddlb_from_dac001 (dropdownlistbox ddlb_parm);string 	ls_add_data,ls_add_name,ls_select_string
int 		li_pos

// Set up select statement using passed parameters
ls_select_string = "SELECT DCODE,DNAME FROM PBCOMMON.DAC001 WHERE " + &
                   "DUSE = ' ' AND DTODT = 0 AND DACTTODT = 0  AND DCODE BETWEEN '1000' AND '9999' " 
                   
PREPARE sqlsa FROM :ls_select_string;

DECLARE dyn_cursor DYNAMIC CURSOR FOR sqlsa;

OPEN DYNAMIC dyn_cursor;  
if sqlca.sqlcode < 0 then
	MessageBox("DataBase Error!", sqlca.sqlerrtext)
	return sqlca.sqlcode
end if 

ddlb_parm.SetRedraw(false)
ddlb_parm.Reset( )
ddlb_parm.AddItem(' ')
Do While sqlca.sqlcode = 0
	Fetch dyn_cursor into :ls_add_data,:ls_add_name;
	if sqlca.sqlcode = 0 then
		ls_add_name = string(ls_add_name,"@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@")
		ddlb_parm.AddItem(ls_add_data + '  ' + ls_add_name)
	elseif sqlca.sqlcode < 0 then
		MessageBox("DataBase Error", sqlca.sqlerrtext)
		return sqlca.sqlcode
	else
		exit
	end if
Loop
ddlb_parm.SetRedraw(true)
Close dyn_cursor;

return 0
end function

