$PBExportHeader$f_up_ipis_mis_tshipex.srf
$PBExportComments$이체분할 불출정보(ipis -> as/400)
global type f_up_ipis_mis_tshipex from function_object
end type

forward prototypes
global function integer f_up_ipis_mis_tshipex (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[])
end prototypes

global function integer f_up_ipis_mis_tshipex (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[]);String ls_srno, ls_srno2, ls_xplant, ls_div, ls_itno, ls_xplant1, ls_div1, ls_isdt, ls_prtdt, ls_slno
Dec{0} ld_aqty
Int li_index, li_cnt, li_rowcnt
str_ipis_server lstr_ipis

//***AS400 접속여부 체크, 저녁8-아침8 인터페이스 저장
li_index = f_ipis_server_get_transaction(ag_str_ipis, 'X', 'Z')

If li_index = -1 then
	ag_message = "A/S400 서버 연결에 실패하였습니다."
	return -1
End if

lstr_ipis = ag_str_ipis[li_index]

//*** 업무로직 적용
for li_cnt = 1 to ag_dwo.rowcount()
	ls_srno    = trim(ag_dwo.getitemstring(li_cnt,"srno"))
	ls_slno    = trim(ag_dwo.getitemstring(li_cnt,"slno"))
	ls_xplant  = trim(ag_dwo.getitemstring(li_cnt,"xplant"))
	ls_div     = trim(ag_dwo.getitemstring(li_cnt,"div"))
	ls_itno    = trim(ag_dwo.getitemstring(li_cnt,"itno"))
	ls_xplant1 = trim(ag_dwo.getitemstring(li_cnt,"xplant1"))
	ls_div1    = trim(ag_dwo.getitemstring(li_cnt,"div1"))
	ls_isdt    = trim(ag_dwo.getitemstring(li_cnt,"isdt"))
	ls_prtdt   = trim(ag_dwo.getitemstring(li_cnt,"prtdt"))
	ld_aqty    = ag_dwo.getitemnumber(li_cnt,"aqty")
	
	ls_srno = mid(ls_srno,1,10)
	
	SELECT Count(*)
	 Into :li_rowcnt
   FROM PBINV.INV621                                                          
   WHERE COMLTD = '01' AND SRNO = :ls_srno  AND SLNO = :ls_slno Using lstr_ipis.t_sqlpis;                                   
                                                                                
  	If li_rowcnt <> 0 then                                                          
      ag_message = "기 불출정보가 존재 합니다."
		return -1                                                           
   End if	
	
	ls_isdt  = f_dateedit_inv(ls_isdt)
	ls_prtdt = f_dateedit_inv(ls_prtdt)
	
	if f_increase_pdnr(ls_srno, ls_srno2) = -1 then
		ag_message = "분할회수 초과 오류"
		return -1
	End if
	
	
	INSERT INTO PBINV.INV621
		( comltd, srno,  srno2, slno, xplant,    div, itno,  gubun, xplant1, div1,
		  isdt,   prtdt, aqty,  stcd, inptid, inptdt, updtid, updtdt)
	Values( '01', :ls_srno, :ls_srno2, :ls_slno, :ls_xplant1, :ls_div1, :ls_itno, 'A', :ls_xplant, :ls_div, 
	       :ls_isdt, :ls_prtdt, :ld_aqty, '3', :g_s_empno,  :g_s_date, :g_s_empno, :g_s_datetime)
	using lstr_ipis.t_sqlpis;
	
	if lstr_ipis.t_sqlpis.sqlnrows < 1 then
		ag_message = "이체불출 디비 Insert Error"
		f_ipis_transaction_errorlog('C','','f_up_ipis_mis_tshipex',&
		lstr_ipis.t_sqlpis.sqlerrtext,'이체불출 DB Insert Error', g_s_empno, g_s_ipaddr, g_s_macaddr)
		return -1
	end if	
next

Return 0
end function

