$PBExportHeader$f_notmodified_chk.srf
$PBExportComments$필수수정항목 Check Global function
global type f_notmodified_chk from function_object
end type

forward prototypes
global function integer f_notmodified_chk (datawindow ag_dw)
end prototypes

global function integer f_notmodified_chk (datawindow ag_dw);/////////////////////////////////////////////////////////////////////////////////////////////
// * 수정되지않은 필수수정 항목 Check *
//
//
// 용도: f_Mandatory_Chk와는 달리 DataWindow Rerieve 후 필수적으로 수정되야하는 항목 Check. 
//
//																				2002.10.31    박병주
/////////////////////////////////////////////////////////////////////////////////////////////

ag_dw.AcceptText()

String 	ls_ColCnt
Long 		ll_RowCnt

ls_ColCnt = ag_dw.Object.DataWindow.Column.Count
ll_RowCnt = ag_dw.RowCount()

Long		ll_Row = 0, ll_i
String	ls_ColName, ls_ColText, ls_Color_Syntax, ls_Color

Do While ll_Row <= ll_RowCnt
	ll_Row = ag_dw.GetNextModified(ll_Row, Primary!)
	If ll_Row = 0 Then Exit
	
	For ll_i = 1 To Dec(ls_ColCnt) 
		
		ls_ColName = ag_dw.Describe('#' + String(ll_i) + '.Name')		
		
		ls_Color_Syntax = ag_dw.Describe(ls_ColName + '.BackGround.Color')
		
		If Pos( ls_Color_Syntax, 'if', 1 ) = 0 Then
			ls_Color = ls_Color_Syntax
		Else				
			ls_Color_Syntax = Mid( ls_Color_Syntax, Pos( ls_Color_Syntax, 'if', 1 ) )
			ls_Color_Syntax = Left( ls_Color_Syntax, Len(ls_Color_Syntax) - 1 )			
			ls_Color = ag_dw.Describe( "Evaluate('" + ls_Color_Syntax +"', " + String(ll_Row) + ")" )
		End If
			
		If Long(ls_Color) = 15780518 Then  // 배경색이 하늘색.
		
			If ag_dw.GetItemStatus( ll_Row, ls_ColName, Primary! ) = NotModified! Then
				ls_ColText = ag_dw.Describe(ls_ColName + '_t.Text')
				MessageBox('확인!', ls_ColText + '는(은) 필수수정 항목입니다.', Exclamation!)
				ag_dw.SetRow(ll_Row)
				ag_dw.SetColumn(ll_i)
				ag_dw.SetFocus( )
				
				Return -1
			End If
			
		End If
		
	Next
	
Loop

Return 1
end function

