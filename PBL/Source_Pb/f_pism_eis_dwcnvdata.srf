$PBExportHeader$f_pism_eis_dwcnvdata.srf
global type f_pism_eis_dwcnvdata from function_object
end type

forward prototypes
global function integer f_pism_eis_dwcnvdata (datawindow adw_eiscnv)
end prototypes

global function integer f_pism_eis_dwcnvdata (datawindow adw_eiscnv);Integer li_buffer 
Long ll_row
DWBuffer l_buf
DWItemStatus l_rowstatus
String ls_sqlaction, ls_tbName, ls_PrimaryData, &
		 ls_AreaCode, ls_divCode, ls_WorkCenter, ls_WorkDay, ls_dailyStatus 

ls_tbName = adw_EIScnv.Describe("Datawindow.table.updatetable")
ls_tbName = Upper(ls_tbName); If ls_tbName = '' Then Return -1 
			
li_buffer = 0
ll_row = 0
Do WHILE TRUE
	IF ll_Row = 0 THEN
		li_buffer++
		CHOOSE CASE li_buffer
			CASE 1
				l_buf = delete!
			CASE 2 
				l_buf = primary!
			CASE 3
				l_buf = filter!
			CASE 4
				EXIT
		END CHOOSE
	END IF
	IF l_buf = delete! THEN
		IF ll_row >= adw_EIScnv.DeletedCount() THEN
			ll_row = 0
			CONTINUE
		END IF
		ll_row++
	ELSE
		ll_row = adw_EIScnv.GetNextModified(ll_row,l_buf)
		IF ll_row <= 0 THEN CONTINUE
	END IF
	// SQL¹® °áÁ¤
	IF l_buf = delete! THEN
		ls_sqlaction = "DELETE"
	ELSE
		l_rowstatus = adw_EIScnv.GetItemStatus(ll_row, 0, l_buf)
		CHOOSE CASE l_rowstatus
			CASE newmodified!
				ls_sqlaction = "INSERT"
			CASE datamodified!
				ls_sqlaction = "UPDATE"
			CASE ELSE
				CONTINUE
		END CHOOSE				
	END IF

	CHOOSE CASE ls_sqlaction
		CASE "INSERT", "UPDATE" 

			adw_EIScnv.SetItem(ll_row, "lastemp", 'Y')
			adw_EIScnv.SetItem(ll_row, "lastdate", f_pisc_get_date_nowtime())

	CASE "DELETE"
			
			Choose Case ls_tbName 
				Case 'TMHDAILYDETAIL' 
					ls_primaryData  = adw_EIScnv.Object.areacode.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.divisioncode.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.workcenter.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.workday.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.empgubun.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.mhgubun.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.mhcode.Delete.original[ll_row] 
					
				Case 'TMHDAILYSUB' 
					
					ls_primaryData  = adw_EIScnv.Object.areacode.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.divisioncode.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.workcenter.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.workday.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.mhgubun.Delete.original[ll_row] + '/'
					ls_primaryData += adw_EIScnv.Object.mhcode.Delete.original[ll_row] + '/'
					ls_primaryData += Trim(String(adw_EIScnv.Object.seqno.Delete.original[ll_row])) 

				Case Else
			End Choose 
			
			If ls_PrimaryData <> '' Then 
				INSERT INTO TDELETE ( TableName, DeleteData, DeleteTime, LastEmp )  
					  VALUES ( :ls_tbName, :ls_PrimaryData, getdate(), :g_s_empno) Using SqlPIS ;
				f_pism_sqlchkopt(False) 
			End If 
			
	END CHOOSE 
LOOP

Return 1 
end function

