$PBExportHeader$f_pisc_dw_check_duplicate.srf
$PBExportComments$데이타의 중복 체크.
global type f_pisc_dw_check_duplicate from function_object
end type

forward prototypes
global function boolean f_pisc_dw_check_duplicate (datawindow adw_target, string as_col_name, long al_current_row, string as_data)
end prototypes

global function boolean f_pisc_dw_check_duplicate (datawindow adw_target, string as_col_name, long al_current_row, string as_data);/**********************************************************************************
* FUNCTION NAME: f_check_duplicate
* 기능         : 중복여부 체크
* Version      : 1.0 							
* 작성일       : 1997.02.12
* 작성자       : 배 홍 길
* INPUT ARG    : 1) adw_target(DATAWINDOW) : 체크할 datawindow name
*              : 2) as_col_name(STRING)    : 체크할 컬럼명 
*              : 3) al_current_rows(LONG)  : 현재행의 번호
*              : 4) as_data(STRING)        : 찾을 데이타값
* RETURN       : BOOLEAN ( 중복일경우 : TRUE,  중복이아닌경우 : FALSE
* 기능 개요    : adw_target datawindow의 as_col_name 컬럼에서 as_data 값과 같은 값이 
*              : 존재하는지 여부를 체크한다.
***********************************************************************************/

// [ Declare ]----------------------------------------------------------------------
STRING        ls_find
BOOLEAN       lb_dup

ls_find = as_col_name + " = '" + as_data + "'"  // [ 찾을 문자열 ]

IF adw_target.RowCount() > 1 THEN               // [ 행의수가 적어도 2개이상인경우 ]
	CHOOSE CASE al_current_row
		CASE 1                                    // [ 현재행번호가 첫번째행일 경우 ]
			IF adw_target.Find(ls_find,2,adw_target.RowCount()) > 0 THEN
				lb_dup = TRUE
			END IF
		CASE adw_target.RowCount()                // [ 현재행번호가 마지막행일 경우 ]
			IF adw_target.Find(ls_find,1,adw_target.RowCount() - 1) > 0 THEN
				lb_dup = TRUE
			END IF
		CASE ELSE                                  // [ 현재행번호가 가운데행일 경우 ]
			IF adw_target.Find(ls_find,1,al_current_row - 1) > 0 OR &
				adw_target.Find(ls_find,al_current_row + 1,adw_target.RowCount()) > 0 THEN
				lb_dup = TRUE
			END IF
	END CHOOSE
	IF lb_dup THEN                                 // [ 중복이 발생한 경우 ]
		f_pisc_dw_rowset(adw_target, al_current_row, as_col_name) // [ 중복행으로 지정 ]
		RETURN TRUE
	END IF
END IF

RETURN FALSE
end function

