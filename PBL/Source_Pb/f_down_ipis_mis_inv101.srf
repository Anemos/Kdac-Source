$PBExportHeader$f_down_ipis_mis_inv101.srf
$PBExportComments$모델마스타 ( inv101 - TMISINV101 공통 )
global type f_down_ipis_mis_inv101 from function_object
end type

forward prototypes
global function integer f_down_ipis_mis_inv101 (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[])
end prototypes

global function integer f_down_ipis_mis_inv101 (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[]);//**********************
//* 계정이 '23','43'인 경우에 인터페이스에 생성한뒤 스케쥴작업에 의해 설비Mater로 이동함
//* 계정이 '21','41'인 품목은 제외
//* 나머지 계정은 해당공장으로 이동시킴
//**********************
string ls_chgdate, ls_chgcd, ls_xplant, ls_div, ls_pdcd, ls_pgroup, ls_mgroup, ls_itno
string ls_cls, ls_srce, ls_xunit, ls_abccd, ls_wloc, ls_xplan, ls_mcno
dec{0} lc_convqty, lc_saud
dec{1} lc_minq, lc_maxq
integer	li_cnt, li_index
str_ipis_server lstr_ipis

for li_cnt = 1 to ag_dwo.rowcount()
	ls_chgcd = trim(ag_dwo.getitemstring(li_cnt,"chgcd"))
	ls_xplant = trim(ag_dwo.getitemstring(li_cnt,"xplant"))
	ls_div = trim(ag_dwo.getitemstring(li_cnt,"div"))
	ls_itno = trim(ag_dwo.getitemstring(li_cnt,"itno"))
	ls_pdcd = trim(ag_dwo.getitemstring(li_cnt,"pdcd"))
	if isnull(ls_pdcd) then ls_pdcd = ''
	ls_pgroup = mid(ls_pdcd,1,2)
	if len(mid(ls_pdcd,1,3)) = 2 then
		ls_mgroup = ls_pdcd + '9'
	else
		ls_mgroup = mid(ls_pdcd,1,3)
	end if
	ls_cls = trim(ag_dwo.getitemstring(li_cnt,"cls"))
	ls_srce = trim(ag_dwo.getitemstring(li_cnt,"srce"))
	ls_xunit = trim(ag_dwo.getitemstring(li_cnt,"xunit"))
	if isnull(ls_xunit) then ls_xunit = ''
	lc_convqty = ag_dwo.getitemnumber(li_cnt,"convqty")
	lc_saud = ag_dwo.getitemnumber(li_cnt,"saud")
	ls_abccd = trim(ag_dwo.getitemstring(li_cnt,"abccd"))
	if isnull(ls_abccd) then ls_abccd = ''
	ls_wloc = trim(ag_dwo.getitemstring(li_cnt,"wloc"))
	if isnull(ls_wloc) then ls_wloc = ''
	ls_xplan = trim(ag_dwo.getitemstring(li_cnt,"xplan"))
	if isnull(ls_xplan) then ls_xplan = ''
	ls_mcno = trim(ag_dwo.getitemstring(li_cnt,"mcno"))
	if isnull(ls_mcno) then ls_mcno = ''
	SELECT CHAR(CURRENT TIMESTAMP) Into :ls_chgdate FROM PBCOMMON.COMM000 FETCH FIRST 1 ROW ONLY;
	
	if ls_cls = '23' or ls_cls = '43' then
		li_index = f_ipis_server_get_transaction(ag_str_ipis, 'X', 'X')
	else
		li_index = f_ipis_server_get_transaction(ag_str_ipis, ls_xplant, ls_div)
	end if
	if li_index = -1 then
		ag_message = "공장연결에 실패하였습니다."
		return -1
	else
		lstr_ipis = ag_str_ipis[li_index]
	end if
		
	if ( ls_cls = '23' or ls_cls = '43' ) then
		SELECT MINQ, MAXQ INTO :lc_minq, :lc_maxq
		FROM PBINV.INV101
		WHERE COMLTD = '01' AND XPLANT = :ls_xplant AND 
				DIV = :ls_div AND ITNO = :ls_itno
		using sqlca;
		
		INSERT INTO TMISINV101(CHGDATE, CHGCD, XPLANT, DIV, 
			PDCD, ITNO, CLS, SRCE, XUNIT, CONVQTY, SAUD, ABCCD, WLOC, 
			XPLAN, MCNO, MINQ, MAXQ, STSCD, DOWNDATE)
		VALUES ( :ls_chgdate, :ls_chgcd, :ls_xplant, :ls_div,
			:ls_pdcd, :ls_itno, :ls_cls, :ls_srce, :ls_xunit, :lc_convqty, :lc_saud, :ls_abccd, :ls_wloc,
			:ls_xplan, :ls_mcno, :lc_minq, :lc_maxq, 'N', getdate() )
		using lstr_ipis.t_sqlpis;
		
		if lstr_ipis.t_sqlpis.sqlcode <> 0 then
			ag_message = "공무자재 인터페이스 생성에 실패하였습니다."
			f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
				lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
			return -1
		end if
		
		continue
	elseif ( ls_cls = '21' or ls_cls = '41' ) then
		continue
	end if
	
	choose case ls_chgcd
		case 'C'
			SELECT ItemCode INTO :ls_itno
			FROM TMSTMODEL
			WHERE AreaCode = :ls_xplant AND DivisionCode = :ls_div AND ItemCode = :ls_itno
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlcode = 0 then
				DELETE FROM TMSTMODEL
				WHERE AreaCode = :ls_xplant AND DivisionCode = :ls_div AND ItemCode = :ls_itno
				using lstr_ipis.t_sqlpis;
			end if
			
			INSERT INTO TMSTMODEL
  				(AreaCode,  DivisionCode, ProductGroup, ModelGroup, ProductCode, ItemCode,
  				ItemClass,  ItemBuySource,  ItemUnit, ConvertFactor, AverageUnitCost, AbcCode,
  				Location, Planner,  LastEmp)
			VALUES( :ls_xplant, :ls_div, :ls_pgroup, :ls_mgroup, :ls_pdcd, :ls_itno,
				:ls_cls, :ls_srce, :ls_xunit, :lc_convqty, :lc_saud, :ls_abccd,
  				:ls_wloc,  :ls_xplan, :g_s_empno )
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlcode <> 0 then
				ag_message = "품목모델정보 생성에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
					lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
			
			SELECT ItemCode INTO :ls_itno
			FROM TMSTITEMCOST
			WHERE AreaCode = :ls_xplant AND DivisionCode = :ls_div AND ItemCode = :ls_itno
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlcode = 0 then
				DELETE FROM TMSTITEMCOST
				WHERE AreaCode = :ls_xplant AND DivisionCode = :ls_div AND ItemCode = :ls_itno
				using lstr_ipis.t_sqlpis;
			end if
			
			INSERT INTO TMSTITEMCOST
  				( AreaCode,  DivisionCode, ItemCode,
  				ItemClass,  ItemBuySource,  ItemUnit, UnitCost, LastEmp )
			VALUES ( :ls_xplant, :ls_div, :ls_itno,
  				:ls_cls, :ls_srce, :ls_xunit, :lc_saud, :g_s_empno )
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlcode <> 0 then
				ag_message = "품목단가정보 생성에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
				lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
		case 'U'
			
			UPDATE TMSTMODEL
			SET ProductGroup	= :ls_pgroup,
				ModelGroup		= :ls_mgroup,
				ProductCode		= :ls_pdcd,
				ItemClass =  :ls_cls,
				ItemBuySource = :ls_srce,
				ItemUnit = :ls_xunit,
				ConvertFactor = :lc_convqty,
				AverageUnitCost = :lc_saud,
				AbcCode = :ls_abccd,
				Location = :ls_wloc,
				Planner = :ls_xplan,
				LastEmp = :g_s_empno,
				LastDate = Getdate()
			WHERE	AreaCode	= :ls_xplant AND	DivisionCode	= :ls_div AND	
					ItemCode	= :ls_itno 
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlnrows < 1 then
				ag_message = "품목모델정보 수정에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
					lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
			
			UPDATE TMSTITEMCOST
			SET ItemClass =  :ls_cls,
				ItemBuySource = :ls_srce,
				ItemUnit = :ls_xunit,
				UnitCost = :lc_saud,
				LastEmp = :g_s_empno,
				LastDate = Getdate()
			WHERE	AreaCode	= :ls_xplant 	AND	DivisionCode	= :ls_div AND	
						ItemCode	= :ls_itno 
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlnrows < 1 then
				ag_message = "품목단가정보 수정에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
					lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
				
		case 'D'
			
			DELETE TMSTMODEL
			WHERE	AreaCode	= :ls_xplant AND	DivisionCode	= :ls_div AND	
					ItemCode	= :ls_itno 
			using lstr_ipis.t_sqlpis;
			
			if lstr_ipis.t_sqlpis.sqlcode <> 0 then
				ag_message = "품목모델정보 삭제에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
					lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
			
			DELETE TMSTITEMCOST
			WHERE	AreaCode	= :ls_xplant AND	DivisionCode	= :ls_div AND	
					ItemCode	= :ls_itno 
			using lstr_ipis.t_sqlpis;
			
			if 	lstr_ipis.t_sqlpis.sqlcode <> 0 then
				ag_message = "품목모델정보 삭제에 실패하였습니다."
				f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv101',&
					lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_xplant + ls_div + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
				return -1
			end if
				
		case else
			ag_message = "데이타구분 CUD에 해당하지 않습니다."
			return -1
	end choose
next

return 0
end function

