$PBExportHeader$f_pisr_get_incometime.srf
$PBExportComments$정산마감일자 고려한 입고일시계산
global type f_pisr_get_incometime from function_object
end type

forward prototypes
global function datetime f_pisr_get_incometime (transaction ftr_tran, string fs_area, string fs_supp, datetime fdt_nowtime)
end prototypes

global function datetime f_pisr_get_incometime (transaction ftr_tran, string fs_area, string fs_supp, datetime fdt_nowtime);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		: f_pisr_get_incometime
//	Access		: reorder
//	Arguments	: transaction	ftr_tran
//					: string			fs_area
//					: string			fs_supp
//					: datetime		fdt_nowtime
//	Returns		: datetime 		입고시간		
//	Description	: 외주업체별 정산 마감일자 고려한 입고일시 계산
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Gwon Oh Sang
// Coded Date	: 2002.09.26
///////////////////////////////////////////////////////////////////////////////////////////////////////////
String	ls_nowTime, ls_lastday, ls_subDate, ls_date, ls_time, ls_nextday, ls_jsCode
DateTime	ldt_incomeTime

ldt_incomeTime	= fdt_nowtime
ls_nowTime 		= String(fdt_nowtime, "yyyy.mm.dd hh:mm")
ls_subDate		= left(ls_nowTime, 7)

//월말일자
  SELECT Convert(char(10), DateAdd(DD, -1, DateAdd(MM, 1, Convert(DateTime, :ls_subDate + '.01'))), 102)
    INTO :ls_lastday  
    FROM TMSTAREA
 	WHERE AreaCode = :fs_area
	USING ftr_tran	;
	
If ls_lastday = left(ls_nowTime, 10) Then	//월말이면
	ls_time	= Mid(ls_nowTime,12, 5)
	If ls_time > '20:00' Then 
	  SELECT Convert(char(10), DateAdd(MM, 1, Convert(DateTime, :ls_subDate + '.01')), 102)
		 INTO :ls_nextday  
		 FROM TMSTAREA
		WHERE AreaCode = :fs_area
		USING ftr_tran	;

		ldt_incomeTime	= DateTime( Date(ls_nextday), Time('08:00'))
	End If
Else	
//////////////////////////////////////////////////////////////////////////////////////////
//입고 처리시 입고일자 가 해당업체의 마감일자 이고 마감시간 이후이면 명일을 입고일자로 봄
//////////////////////////////////////////////////////////////////////////////////////////
	  SELECT TMSTSUPPLIER.JSCode  
		 INTO :ls_jsCode  
		 FROM TMSTSUPPLIER  
		WHERE TMSTSUPPLIER.SupplierCode = :fs_supp
		USING ftr_tran	;
	
	If ls_jsCode = 'H' Then 			//중간정산(15일정산)마감이면 
		ls_date	= Mid(ls_nowTime, 9, 2)
		ls_time	= Mid(ls_nowTime,12, 5)
		If ls_date = '15' And ls_time > '20:00' Then 
			ldt_incomeTime	= DateTime( Date(ls_subDate + '.16'), Time('08:00'))
		End If
	End If
///////////////////////////////////////////////////////////////////////////////////////////
End If

Return ldt_incomeTime
end function

