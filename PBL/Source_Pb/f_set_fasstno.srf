$PBExportHeader$f_set_fasstno.srf
$PBExportComments$고정자산 폐기확정및 확정취소
global type f_set_fasstno from function_object
end type

forward prototypes
global function integer f_set_fasstno (string ag_fasstno, string ag_empno, string ag_date, string ag_gubun)
end prototypes

global function integer f_set_fasstno (string ag_fasstno, string ag_empno, string ag_date, string ag_gubun);/*		해당 자산코드에 해당하는 금형 폐기확정(경리팀) Update ( pur707 , pur708 )	
	
	Argument : 
					String 	ag_fasstno 		: 고정자산코드
					String   ag_empno			: 폐기확정자 사번
					String   ag_date			: 폐기확정 일자
					String   ag_gubun       : 구분    - S : 폐기확정
															      C : 폐기확정 취소

   Success    1
	Fail      -1	: 외주금형 이력(폐기)
	          -2 	: 외주금형 진행
				 -3	: 외주금형 MASTER 불용여부 
*/
ag_fasstno = Upper(Trim(ag_fasstno))
ag_empno = Trim(ag_empno)	//사번
ag_date = Trim(ag_date)		//폐기확정일
ag_gubun = Upper(Trim(ag_gubun))

String ls_time
ls_time = Left(g_s_time,2) + Mid(g_s_time,4,2) + Right(g_s_time,2)

//폐기확정
IF ag_gubun = "S" Then
	
	//외주금형 이력
	UPDATE PBPUR.PUR707
	SET COMPLN = :ag_empno , COMDT = :ag_date, COMTM = :ls_time, COMCD = 'C'
	WHERE COMLTD = '01'
	  AND SLIPTYPE = 'SS'
	  AND STCD = '3'	//폐기승인
	  AND TRIM(PATTNO)||TRIM(PATTSER) IN 
			(SELECT TRIM(PATTNO)||TRIM(PATTSER)
			 FROM PBPUR.PUR708
			 WHERE COMLTD = '01'
				AND FASSTNO = :ag_fasstno)
	Using sqlca ;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -1
	End IF
	
	//외주금형 진행
	UPDATE PBPUR.PUR708
	 SET COMDT = :ag_date, STCD = '6'
	WHERE COMLTD = '01'
	  AND STCD = '5'  //폐기중
	  AND FASSTNO = :ag_fasstno 
	Using sqlca;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -2
	End IF
	
	//외주금형 MASTER
	UPDATE PBPUR.PUR701
	 SET XSTOP = 'C'
	WHERE COMLTD = '01'
	  AND TRIM(PATTNO)||TRIM(PATTSER) IN 
			(SELECT TRIM(PATTNO)||TRIM(PATTSER)
			 FROM PBPUR.PUR708
			 WHERE COMLTD = '01'
				AND FASSTNO = :ag_fasstno)
	Using sqlca ;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -3
	End IF
	
Else
	//폐기 확정 취소
	
	//외주금형 이력
	UPDATE PBPUR.PUR707
	SET COMPLN = ' ', COMDT = ' ', COMTM = ' ' , COMCD = ' '
	WHERE COMLTD = '01'
	  AND SLIPTYPE = 'SS'
	  AND STCD = '3'	//폐기승인
	  AND COMCD = 'C' //경리폐기확정
	  AND TRIM(PATTNO)||TRIM(PATTSER) IN 
			(SELECT TRIM(PATTNO)||TRIM(PATTSER)
			 FROM PBPUR.PUR708
			 WHERE COMLTD = '01'
				AND FASSTNO = :ag_fasstno)
	Using sqlca ;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -1
	End IF
	
	//외주금형 진행
	UPDATE PBPUR.PUR708
	 SET COMDT = ' ', STCD = '5'
	WHERE COMLTD = '01'
	  AND STCD = '6'  //폐기확정
	  AND FASSTNO = :ag_fasstno 
	Using sqlca;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -2
	End IF
	
	//외주금형 MASTER
	UPDATE PBPUR.PUR701
	 SET XSTOP = ' '
	WHERE COMLTD = '01'
	  AND TRIM(PATTNO)||TRIM(PATTSER) IN 
			(SELECT TRIM(PATTNO)||TRIM(PATTSER)
			 FROM PBPUR.PUR708
			 WHERE COMLTD = '01'
				AND FASSTNO = :ag_fasstno)
	Using sqlca ;
	
	IF sqlca.sqlcode <> 0 OR sqlca.SqlNRows < 1 Then
		Return -3
	End IF
	
End IF

Return 1
end function

