$PBExportHeader$f_down_ipis_mis_inv002.srf
$PBExportComments$품목마스타 ( inv002 - TMISINV002 공통 )
global type f_down_ipis_mis_inv002 from function_object
end type

forward prototypes
global function integer f_down_ipis_mis_inv002 (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[])
end prototypes

global function integer f_down_ipis_mis_inv002 (ref string ag_message, datawindow ag_dwo, any ag_str_ipis[]);string ls_chgdate, ls_chgcd, ls_itno, ls_itnm, ls_spec, ls_xunit, ls_xtype, ls_rvno
integer	li_cnt, li_index
str_ipis_server lstr_ipis

for li_cnt = 1 to ag_dwo.rowcount()
	ls_chgcd = trim(ag_dwo.getitemstring(li_cnt,"chgcd"))
	ls_itno = trim(ag_dwo.getitemstring(li_cnt,"itno"))
	ls_itnm = trim(ag_dwo.getitemstring(li_cnt,"itnm"))
	ls_spec = trim(ag_dwo.getitemstring(li_cnt,"spec"))
	ls_xunit = trim(ag_dwo.getitemstring(li_cnt,"xunit"))
	if isnull(ls_xunit) then ls_xunit = ''
	ls_xtype = trim(ag_dwo.getitemstring(li_cnt,"xtype"))
	if isnull(ls_xtype) then ls_xtype = ''
	ls_rvno = trim(ag_dwo.getitemstring(li_cnt,"rvno"))
	if isnull(ls_rvno) then ls_rvno = ''
	// 먼저 시간대를 체크한다.
	SELECT CHAR(CURRENT TIMESTAMP) Into :ls_chgdate FROM PBCOMMON.COMM000 FETCH FIRST 1 ROW ONLY
	using sqlca;
	
	li_index = f_ipis_server_get_transaction(ag_str_ipis, 'X', 'X')	
	if li_index = -1 then
		ag_message = "공장연결에 실패하였습니다."
		return -1
	else
		lstr_ipis = ag_str_ipis[li_index]
	end if

	INSERT INTO TMISINV002(CHGDATE, CHGCD, ITNO, ITNM, 
		SPEC, XUNIT, XTYPE, RVNO, STSCD, DOWNDATE)
	VALUES ( :ls_chgdate,	:ls_chgcd,	:ls_itno,	:ls_itnm, 
		:ls_spec, :ls_xunit,	:ls_xtype, :ls_rvno, 'N', getdate())
	using lstr_ipis.t_sqlpis;
			
	if lstr_ipis.t_sqlpis.sqlcode <> 0 then
		ag_message = "품목마스타 생성시에 에러가 발생하였습니다."
		f_ipis_transaction_errorlog(ls_chgcd,g_s_winid,'f_down_ipis_mis_inv002',&
			lstr_ipis.t_sqlpis.sqlerrtext,ag_message + ls_itno, g_s_empno, g_s_ipaddr, g_s_macaddr)
		return -1
	end if					
next

return 0
end function

