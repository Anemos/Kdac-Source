$PBExportHeader$f_ex_request.srf
$PBExportComments$서열공급관련 이체의뢰확정(ag : dw ) return string
global type f_ex_request from function_object
end type

forward prototypes
global function string f_ex_request (datawindow adw_target)
end prototypes

global function string f_ex_request (datawindow adw_target);//**************************************************
// 서열공급관련 이체의뢰확정정보 생성                      
// ag_pass => INV601 insert  
//**************************************************
String  ls_xplant, ls_div, ls_cls, ls_srce, ls_pdcd, ls_gubun, ls_xplant1, ls_div1, ls_cls1, ls_srce1, ls_itno, ls_dudt
String  ls_txplant, ls_tdiv, ls_txplant1, ls_tdiv1, ls_tgubun, ls_tdudt, ls_tpdcd
String  ls_err, ls_key, ls_slno, ls_costdiv, ls_srno, ls_srno1, ls_srno2
Int    li_rtn, ii
Long   ll_row, ll_InsertRow
Dec{0} ld_reqqt
//datastore 선언
Datastore lds_inv601

adw_target.accepttext()
adw_target.setitemstatus(1,0,primary!,datamodified!)

//****************************************************
//  ag_pass data validation check!!
//  8개 항목 필수입력
//  6개 항목 같은 데이타! 품번, 의뢰량은 다르게
//  의뢰, 불출 공장 품번존재 및 품목구분 chek
//****************************************************
// 필수항목 체크~
if f_mandatory_chk(adw_target) = -1 then 
	ls_err = "필수항목입력"
	return ls_err 
End if

ll_row = adw_target.rowcount()

For ii = 1 to ll_row
	
	ls_xplant  = adw_target.GetItemString(ii, 1)
	ls_div     = adw_target.GetItemString(ii, 2)
	ls_xplant1 = adw_target.GetItemString(ii, 3)
	ls_div1    = adw_target.GetItemString(ii, 4)
	ls_gubun   = adw_target.GetItemString(ii, 5)
	ls_dudt    = adw_target.GetItemString(ii, 6)
	ls_itno    = adw_target.GetItemString(ii, 7)
	
	//품목재고(INV101)의 계정구입선 확인("10"-"05") check
	SELECT  A."CLS", A."SRCE", A."PDCD"  
	  INTO :ls_cls, :ls_srce, :ls_pdcd
	  FROM  "PBINV"."INV101" A
	WHERE  A.COMLTD = '01' AND	A.XPLANT = :ls_xplant AND A.DIV = :ls_div AND A.ITNO = :ls_itno 
	Using sqlca;
	
	If sqlca.sqlcode <> 0 Then
		ls_err = "의뢰품번 미존재"
		return ls_err
	End If
	
	If ii <> 1 And ( ls_xplant <> ls_txplant Or ls_div <> ls_tdiv Or ls_xplant1 <> ls_txplant1 Or &
	   ls_div1 <> ls_tdiv1 Or ls_gubun <> ls_tgubun or ls_dudt <> ls_tdudt Or ls_pdcd <> ls_tpdcd  ) Then
		ls_err = "기본정보상이함"
		return ls_err		
	End if	
	
	SELECT  A."CLS", A."SRCE"  
	  INTO :ls_cls1, :ls_srce1
	  FROM  "PBINV"."INV101" A
	WHERE  A.COMLTD = '01' AND	A.XPLANT = :ls_xplant1 AND A.DIV = :ls_div1 AND A.ITNO = :ls_itno 
	Using sqlca;
	
	If sqlca.sqlcode <> 0 Then
		ls_err = "불출품번 미존재"
		return ls_err
	End If
	
	if ls_cls = '10' and ls_srce = '05' then		
	  if (ls_cls1 <> '30') then
		  if ( ls_cls1 = '10' and ls_srce1 <> '03') then
			  ls_err = "계정확인"
				return ls_err
		  end if
		end if
	end if
	
	if ls_cls = '10' and ls_srce =  '06' then
	  if ((ls_cls1 <> '10' and ls_srce1 <> '04') or ( ls_cls1 <> '40' and ls_srce1 <> '04')  &
	     or ( ls_cls1 <> '50' and ls_srce1 <> '04')) then
				ls_err = "계정확인"
				return ls_err
	  end if
	end if
	
	//품목구분별 editing
	if ls_gubun = 'A' then
		if Not(ls_cls = '10' And ls_srce = '05') then
			ls_err = "품목구분확인"
			return ls_err
	  	end if
	end if
	if ls_gubun = 'B' then
		if Not(ls_cls = '10' And ls_srce = '06') then
			ls_err = "품목구분확인"
			return ls_err
	  end if
	end if
	if ls_gubun = 'C' then
		if ls_cls = '10' then
			if Not(ls_srce = '01' or ls_srce = '02' or ls_srce = '03' or ls_srce = '04')  then
				ls_err = "품목구분확인"
				return ls_err				
			end if
		else
			ls_err = "품목구분확인"
			return ls_err		
		end if
	end if
	
	ls_txplant  = ls_xplant
	ls_tdiv     = ls_div
	ls_txplant1 = ls_xplant1
	ls_tdiv1    = ls_div1
	ls_tgubun   = ls_gubun
	ls_tdudt    = ls_dudt
	ls_tpdcd    = ls_pdcd
	
Next

//INV601 DataStore Open
lds_inv601 = create datastore                  			              
lds_inv601.dataobject = "d_ex_inv601"
lds_inv601.settransobject(sqlca)

// 이체전표 번호부여
ls_key = 'RW' + mid(g_s_date,4,3)
ls_slno = f_get_slno1(ls_key, 'EX')	

For ii = 1 to ll_row
	
	ll_InsertRow = lds_inv601.InsertRow(0)
	
	ls_itno   = adw_target.GetItemString(ii, 7)         // 입력 품번
	ld_reqqt  = adw_target.GetItemDecimal(ii, 8)        // 입력 의뢰수량
	
		SELECT  "CLS", "SRCE"  
		  INTO  :ls_cls, :ls_srce
		  FROM  "PBINV"."INV101" 
		WHERE  "COMLTD" = '01' AND	"XPLANT" = :ls_xplant AND
		       "DIV" = :ls_div AND "ITNO" = :ls_itno 
		Using sqlca;
	
	
	lds_inv601.setitem(ll_InsertRow,'comltd','01')
	lds_inv601.setitem(ll_InsertRow,'xplant',ls_xplant)
	lds_inv601.setitem(ll_InsertRow,'div',ls_div)

	ls_key = 'RW' + ls_xplant + ls_div + ls_cls + mid(g_s_date,4,3)
	ls_srno = f_get_ctlno(ls_key)
	ls_srno1 = ls_xplant + ls_div
	ls_srno2 = ls_cls
	
	lds_inv601.setitem(ll_InsertRow,'srno',ls_srno + ls_srno1 + ls_srno2)
	
	lds_inv601.setitem(ll_InsertRow,'itno',ls_itno)
	lds_inv601.setitem(ll_InsertRow,'reqqt',ld_reqqt)
	lds_inv601.setitem(ll_InsertRow,'dudt',ls_dudt)
	lds_inv601.setitem(ll_InsertRow,'gubun',ls_gubun)
	lds_inv601.setitem(ll_InsertRow,'pdcd',ls_pdcd)
	// 원가사업장
   ls_costdiv = f_get_accdiv(ls_xplant,ls_div,ls_pdcd)
	lds_inv601.setitem(ll_InsertRow,'costdiv',ls_costdiv)
	lds_inv601.setitem(ll_InsertRow,'xplant1',ls_xplant1)
	lds_inv601.setitem(ll_InsertRow,'div1',ls_div1)
	lds_inv601.setitem(ll_InsertRow,'reqdt',g_s_date)
	lds_inv601.setitem(ll_InsertRow,'cls',ls_cls)
	lds_inv601.setitem(ll_InsertRow,'srce',ls_srce)
	lds_inv601.setitem(ll_InsertRow,'istcd','S')
	lds_inv601.setitem(ll_InsertRow,'stcd','3')                     //의뢰확정상태
	lds_inv601.setitem(ll_InsertRow,'slno',ls_slno)                 //동일한 전표번호 setting

	f_null_Kill2( lds_inv601 )
	f_ExtColumn_Set2( lds_inv601 ) 
Next

li_Rtn = lds_inv601.Update(True, False)

If li_Rtn = 1 Then   	
	lds_inv601.ResetUpdate( )	
//	Commit Using SQLCA ;
   destroy lds_inv601
Else	
	RollBack Using SQLCA ;
//	destroy lds_inv601
	ls_err = "의뢰정보생성실패"
	return ls_err
End if
return ''
end function

