$PBExportHeader$f_wip_cost_average.srf
$PBExportComments$이동평균단가 계산 Sub Function
global type f_wip_cost_average from function_object
end type

forward prototypes
global function decimal f_wip_cost_average (readonly datastore a_ds, string a_adjdate, string a_predate, long a_curcnt, string a_gubun)
end prototypes

global function decimal f_wip_cost_average (readonly datastore a_ds, string a_adjdate, string a_predate, long a_curcnt, string a_gubun);//****************************************************************************
// 이동평균단가 계산 함수
//****************************************************************************
dec{5} lc_avgcost
dec{4} lc_bgqty, lc_inqty, lc_convqty
dec{0} lc_bgamt, lc_inamt, lc_inamt2, lc_usat9
string ls_cmcd, ls_plant, ls_dvsn, ls_itno, ls_orct, ls_srce, ls_itcl, ls_iocd

if a_gubun = 'wip' then     //재공이월 DB
	ls_cmcd = a_ds.getitemstring(a_curcnt,"wip002_wbcmcd")
	ls_plant = a_ds.getitemstring(a_curcnt,"wip002_wbplant")
	ls_dvsn = a_ds.getitemstring(a_curcnt,"wip002_wbdvsn")
	ls_orct = trim(a_ds.getitemstring(a_curcnt,"wip002_wborct"))
	ls_itno = trim(a_ds.getitemstring(a_curcnt,"wip002_wbitno"))
	ls_iocd = trim(a_ds.getitemstring(a_curcnt,"wip002_wbiocd"))
	ls_srce = a_ds.getitemstring(a_curcnt,"inv402_srce")
	ls_itcl = a_ds.getitemstring(a_curcnt,"inv402_cls")
	if ls_srce = '05' then
		lc_avgcost = 0
		a_ds.setitem(a_curcnt,"wip002_wbavrg1",lc_avgcost)
		return lc_avgcost
	end if
	lc_inqty = a_ds.getitemdecimal(a_curcnt,"wip002_wbinqt")
	lc_bgqty = a_ds.getitemdecimal(a_curcnt,"wip002_wbbgqt")
	lc_inamt = a_ds.getitemdecimal(a_curcnt,"wip002_wbinat1") + a_ds.getitemdecimal(a_curcnt,"wip002_wbinat3")
	//-> 이체포함 투입금액(생략)
	lc_inamt2 = a_ds.getitemdecimal(a_curcnt,"wip002_wbinat2")
	lc_bgamt = a_ds.getitemdecimal(a_curcnt,"wip002_wbbgat1")
	//-> 이체포함 기초금액(생략)
	lc_usat9 = a_ds.getitemdecimal(a_curcnt,"wip002_wbusat9")
else                       //창고재공 DB
	ls_cmcd = a_ds.getitemstring(a_curcnt,"wip003_wccmcd")
	ls_plant = a_ds.getitemstring(a_curcnt,"wip003_wcplant")
	ls_dvsn = a_ds.getitemstring(a_curcnt,"wip003_wcdvsn")
	ls_itno = trim(a_ds.getitemstring(a_curcnt,"wip003_wcitno"))
	ls_srce = a_ds.getitemstring(a_curcnt,"inv402_srce")
	ls_itcl = a_ds.getitemstring(a_curcnt,"inv402_cls")
	lc_inqty = a_ds.getitemdecimal(a_curcnt,"wip003_wcinqt")
	lc_bgqty = a_ds.getitemdecimal(a_curcnt,"com_bgqty")
	lc_inamt = a_ds.getitemdecimal(a_curcnt,"wip003_wcinat1")
	lc_bgamt = a_ds.getitemdecimal(a_curcnt,"wip003_wcbgat1")
end if

if lc_bgqty <> 0 or lc_inqty <> 0 then
	if ( lc_bgamt = 0 and lc_inamt <> 0 and lc_inqty <> 0 ) then
		lc_avgcost = abs(lc_inamt / lc_inqty)
	elseif ( lc_inamt = 0 and lc_bgamt <> 0 and lc_bgqty <> 0 ) then
		lc_avgcost = abs(lc_bgamt / lc_bgqty)
	elseif sign(lc_bgqty) <> sign(lc_inqty) then
		if sign(lc_bgqty) = 1 then
			lc_avgcost = abs(lc_bgamt / lc_bgqty)
		elseif sign(lc_inqty) = 1 then
			lc_avgcost = abs(lc_inamt / lc_inqty)
		else
			lc_avgcost = 0
		end if
	else
		lc_avgcost = abs((lc_bgamt + lc_inamt) / (lc_bgqty + lc_inqty))
	end if
end if

if (a_adjdate = '201101' and ls_plant = 'D' and ls_dvsn = 'M') then
	if ls_itno = '412605' then
		lc_avgcost = 2250
	elseif ls_itno = '412605P' then
		lc_avgcost = 2250
	elseif ls_itno = '412658' then
		lc_avgcost = 3590
	elseif ls_itno = '412658P' then
		lc_avgcost = 3590
	elseif ls_itno = '412659' then
		lc_avgcost = 3590
	elseif ls_itno = '412659P' then
		lc_avgcost = 3590
	end if
end if

if lc_avgcost = 0 then
	if ls_itcl = '10' and (ls_srce = '01' or ls_srce = '02' or ls_srce = '04' or ls_srce = '05' or ls_srce = '06') then
		//원재료인 경우에 마감월의 자재이동평균단가 이용
		SELECT "PBINV"."INV402"."COSTAV" , "PBINV"."INV101"."CONVQTY" 
			INTO :lc_avgcost , :lc_convqty 
			FROM "PBINV"."INV402" , "PBINV"."INV101"  
			WHERE ( "PBINV"."INV402"."COMLTD" = "PBINV"."INV101"."COMLTD" ) AND
					( "PBINV"."INV402"."XPLANT" = "PBINV"."INV101"."XPLANT" ) AND
					( "PBINV"."INV402"."DIV" = "PBINV"."INV101"."DIV" ) AND
					( "PBINV"."INV402"."ITNO" = "PBINV"."INV101"."ITNO" ) AND
					( "PBINV"."INV402"."COMLTD" = :ls_cmcd ) AND  
					( "PBINV"."INV402"."XPLANT" = :ls_plant ) AND  
					( "PBINV"."INV402"."DIV" = :ls_dvsn ) AND  
					( "PBINV"."INV402"."XYEAR" = :a_adjdate ) AND  
					( "PBINV"."INV402"."ITNO" = :ls_itno )  using sqlca;
		if sqlca.sqlcode <> 0 then
			lc_avgcost = 0
		else
			if lc_convqty = 0 then
				lc_convqty = 1
			end if
			lc_avgcost = lc_avgcost / lc_convqty
		end if
	end if
	if lc_avgcost = 0 then
		if a_gubun = 'wip' then
			SELECT "PBWIP"."WIP002"."WBAVRG1"  
				INTO :lc_avgcost  
				FROM "PBWIP"."WIP002"  
				WHERE ( "PBWIP"."WIP002"."WBCMCD" = :ls_cmcd ) AND  
						( "PBWIP"."WIP002"."WBPLANT" = :ls_plant ) AND  
						( "PBWIP"."WIP002"."WBDVSN" = :ls_dvsn ) AND  
						( "PBWIP"."WIP002"."WBORCT" = :ls_orct ) AND  
						( "PBWIP"."WIP002"."WBITNO" = :ls_itno ) AND 
						( "PBWIP"."WIP002"."WBIOCD" = :ls_iocd ) AND
						( "PBWIP"."WIP002"."WBYEAR"||"PBWIP"."WIP002"."WBMONTH" = :a_predate )  using sqlca;
			if sqlca.sqlcode <> 0 then
				lc_avgcost = 0
			end if
		else
			SELECT "PBWIP"."WIP003"."WCAVRG1"  
				INTO :lc_avgcost  
				FROM "PBWIP"."WIP003"  
				WHERE ( "PBWIP"."WIP003"."WCCMCD" = :ls_cmcd ) AND  
						( "PBWIP"."WIP003"."WCPLANT" = :ls_plant ) AND  
						( "PBWIP"."WIP003"."WCDVSN" = :ls_dvsn ) AND    
						( "PBWIP"."WIP003"."WCITNO" = :ls_itno ) AND  
						( "PBWIP"."WIP003"."WCYEAR"||"PBWIP"."WIP003"."WCMONTH" = :a_predate )  using sqlca;
			if sqlca.sqlcode <> 0 then
				lc_avgcost = 0
			end if
		end if
	end if	
end if

if a_gubun = 'wip' then
	a_ds.setitem(a_curcnt,"wip002_wbavrg1",lc_avgcost)
else
	a_ds.setitem(a_curcnt,"wip003_wcavrg1",lc_avgcost)
end if

return lc_avgcost

end function

