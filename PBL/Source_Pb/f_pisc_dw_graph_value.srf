$PBExportHeader$f_pisc_dw_graph_value.srf
$PBExportComments$Graph Value Display Function(arg datawindow fdw_graph, statictext fst_popup, string fs_graph, string fs_unit, Return none)
global type f_pisc_dw_graph_value from function_object
end type

forward prototypes
global function boolean f_pisc_dw_graph_value (datawindow fdw_graph, statictext fst_popup, string fs_graph, string fs_unit)
end prototypes

global function boolean f_pisc_dw_graph_value (datawindow fdw_graph, statictext fst_popup, string fs_graph, string fs_unit);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		: f_graph_value_popup
//	Access		: public
//	Arguments	: fdw_graph	= Graph DataWindow
//					  fst_popup	= Graph Value를 Display 할 StaticText Object
//					  fs_graph	= Graph Object Name
//					  fs_unit	= Graph value의 단위
//	Returns		: Boolean
//	Description	: Graph에 마우스 오른쪽 버튼을 클릭했을때 Graph Value 값을 Display 한다.
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Lee Jong-il
// Coded Date	: 1998. 05. 20
///////////////////////////////////////////////////////////////////////////////////////////////////////////
// Clicked script for dw_headcount
// This will cause a static text box to appear next to the pointer where the user
// is using right mouse button down. The acutal value for the data item will
// be displayed in the text box.

grObjectType	ClickedObject
Int				li_series, li_category, ll_pos
String			ls_data, ls_describe
PowerObject		Parent_Object	
Window			lw_win

If Not IsValid(fdw_graph) Or Not IsValid(fst_popup) Then
	Return False
End If

ls_describe = fdw_graph.Describe("DataWindow.Objects")

// fdw_graph의 Graphic Object 의 Name이 fs_graph 인지 Check
ll_pos = Pos(Upper(ls_describe), Upper(fs_graph))
If ll_pos = 0 Then
	Return False
End If

/************************************************************
	Find out where the user clicked in the graph
 ***********************************************************/
ClickedObject = fdw_graph.ObjectAtPointer (fs_graph, li_series, li_category)

/************************************************************
	If user clicked on data or category, find out which one
 ***********************************************************/
If ClickedObject	= TypeData! Then
	ls_data	= String(fdw_graph.GetData(fs_graph, li_series, li_category))
	
	ll_pos	= Pos(ls_data, '.')
	
	If ll_pos > 0 Then
		ls_data	= String(fdw_graph.GetData(fs_graph, li_series, li_category), '#,##0.00')
	Else
		ls_data	= String(fdw_graph.GetData(fs_graph, li_series, li_category), '#,##0')
	End If
	
	fst_popup.text	=  ls_data + " " + fs_unit
	Parent_Object	= fdw_graph.GetParent()
	GetType:
	If Parent_Object.TypeOf() = Window! Then
		lw_win		= Parent_Object
		fst_popup.x	= lw_win.PointerX() - Truncate(fst_popup.Width * 2 / 3, 0)
		fst_popup.y	= lw_win.PointerY() - Truncate(fst_popup.Height / 2, 0)
		fst_popup.visible = true
	Else
		Parent_Object	= Parent_Object.GetParent()
		Goto GetType
	End If
End If
end function

