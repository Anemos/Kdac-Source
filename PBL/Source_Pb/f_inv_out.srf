$PBExportHeader$f_inv_out.srf
$PBExportComments$불출정보입력(여주 Interface)
global type f_inv_out from function_object
end type

forward prototypes
global function integer f_inv_out (string as_gubun, string as_xplant, string as_div, string as_itno, string as_dept, string as_xuse, string as_date, string as_stcd, decimal ad_qty, string as_pjtno, string as_vsrno, string as_srno, string as_usrid, string as_inptdt, ref string as_error)
end prototypes

global function integer f_inv_out (string as_gubun, string as_xplant, string as_div, string as_itno, string as_dept, string as_xuse, string as_date, string as_stcd, decimal ad_qty, string as_pjtno, string as_vsrno, string as_srno, string as_usrid, string as_inptdt, ref string as_error);//**************************************************
// 여주공장 불출정보 Interface                      
// INV401 INSERT => INV101 UPDATE => 재공함수 CALL  
//**************************************************

// 입력시 변수선언  
String  ls_cls,  ls_srce, ls_unit, ls_pdcd, ls_plan, ls_maln 
String  ls_itnm, ls_spec, ls_type
String  ls_slno, ls_srno, ls_vndr, ls_vndnm
String  ls_key,  ls_key1, ls_cdiv, ls_dept
Long    ll_wamt, ll_amt,  ll_ohamt, ll_outamt, ll_row
Dec{1}  ld_uqty, ld_rqty, ld_sqty,  ld_outqty, ld_ohqty, ld_saqty
Dec{2}  ld_cost
// 삭제시 변수선언
String  ls_dsrno, ls_dsrno1, ls_dsrno2, ls_sltype
Long    ll_tramt
Dec{1}  ld_tqty4
Int     li_cnt

Datastore ds_trans_01

/////////////////////////////////////////
////// Argument Value 유효성검정   //////
////////////////////////////////////////

// 불출일 : 결산년월 Check
If f_yeo_online(as_date,' ') = -1 Then 
	as_error = "마감일후 작업할 수 없음"
	return -1
End If

If as_gubun = 'A' Then
	
	// 불출부서 : 현재사용중인 부서유무
	If as_xuse <> '01' Then
		If as_dept <> "" Then
			If f_yeo_dept_use(as_dept,as_date) = -1 Then 
				as_error = "불출부서 : 현재사용중인 부서가 아님"
				return -1
			End If
		End If
	End If
	
	// Project No : Not Spaces 일때 사용부서가 연구소 & 용도는 기타불출
	If as_pjtno <> ""  Then
		If f_yeo_dept_pjtno(as_dept) = -1 Then
			as_error = "Project No : Not Spaces 일때 사용부서가 연구소여야 함 " 
			return -1
		End If
		If as_xuse <> '06' Then
			as_error = "Project No : Not Spaces 일때 불출용도는 기타불출이여야 함"
			return -1
		End If
	End If
	
	If as_xuse = '07' Then
		SELECT Count("ITNO")	INTO :li_cnt
		  FROM "PBINV"."INV304"  
		WHERE "COMLTD" = '01'    AND "XPLANT" = :as_xplant AND
				"DIV"    = :as_div AND "ITNO"   = :as_itno using sqlca;
					
		If li_cnt < 1 Then          
			as_error = "유상사급단가가 존재하지 않습니다."
			Return -1
		End If		
	End if
	
End If

If as_gubun = 'A' Then
	
	// INV401 Insert 용 DataStore Open
	ds_trans_01 = create datastore                  			              
	ds_trans_01.dataobject = "d_inv_out_01"
	ds_trans_01.settransobject(sqlca)
	
End If

If as_gubun = 'A' Then
	
	// INV401(Trans) 데이타 입력
	ds_trans_01.Reset( )
	ll_row = ds_trans_01.insertrow(0)
	
Else

	// 삭제시 Trans Read => Primary Key
	SELECT "SLIPTYPE", "SRNO",    "SRNO1",    "SRNO2",    "TQTY4",   "TRAMT"
	  INTO :ls_sltype, :ls_dsrno, :ls_dsrno1, :ls_dsrno2, :ld_tqty4, :ll_tramt
	FROM "PBINV"."INV401" 
	WHERE "COMLTD" = '01'     AND "XPLANT" = :as_xplant AND "DIV" = :as_div AND 
	      "ITNO"   = :as_itno AND "TFILL"  = :as_srno  using sqlca ;
	If sqlca.sqlcode <> 0 Then
		as_error = "삭제할 데이타가 존재하지 않음"
		return -1
	End If
	
End If

If as_gubun = 'A' Then
	
	// 품목Master에서 기본정보 가져오기
	SELECT "PBINV"."INV002"."ITNM", "PBINV"."INV002"."SPEC",	"PBINV"."INV002"."XTYPE"  
	 INTO  :ls_itnm, :ls_spec,	:ls_type  
	FROM "PBINV"."INV002"  
	WHERE ( "PBINV"."INV002"."COMLTD" = '01' ) AND ( "PBINV"."INV002"."ITNO" = :as_itno ) using sqlca;
	If sqlca.sqlcode <> 0 Then
		as_error = "품목Master에서 기본정보 조회 error"
		return -1
	End If
	
End If

//품목재고 테이블에서 기존정보가져오기
SELECT "CLS",    "SRCE",     "XUNIT",    "PDCD",   "XPLAN",  "MLAN",   
		 "COSTAV", "OUTQTY",   "OUTAMT",   "OHUQTY", "OHRQTY", "OHSQTY", "OHAMT"  
  INTO :ls_cls,  :ls_srce,   :ls_unit,   :ls_pdcd, :ls_plan, :ls_maln,   
  		 :ld_cost, :ld_outqty, :ll_outamt, :ld_uqty, :ld_rqty, :ld_sqty, :ll_ohamt 
FROM "PBINV"."INV101" 
WHERE "COMLTD" = '01' AND "XPLANT" = :as_xplant AND 
		"DIV" = :as_div AND "ITNO"   = :as_itno   using sqlca ;
 
If sqlca.sqlcode <> 0 Then
	as_error = "품목재고 테이블에서 기존정보가져오기 실패"
	return -1
End If

If as_gubun = 'A' Then
	
	// 전산번호 부여
	ls_key  = 'IS' + as_xplant + as_div + ls_cls + mid(as_date,4,3)  
	ls_srno = f_yeo_get_ctlno(ls_key,as_date)
	
	// 원가사업장
	ls_cdiv = f_get_accdiv(as_xplant, as_div, ls_pdcd)
//	If as_xplant = 'Y' then
//		If as_div = 'Y' then
//			If mid(ls_pdcd,1,2) = '40' or mid(ls_pdcd,1,2) = '45' then
//				ls_cdiv = 'H'
//			Else
//				ls_cdiv = 'D'
//			End If
//		Else
//			ls_cdiv = '*'
//		End If
//	End If	
	// 전표번호 부여
	ls_key1 = as_dept + mid(as_date,4,3)
	ls_slno = f_yeo_get_slno(ls_key1,as_date)

	// 사급업체 : 업체,업체명,결제조건, Not 사급업체 : 불출부서
	If as_xuse = '04' or as_xuse = '07' Then
		ls_Vndr  = f_Yeo_Vndr_Pur( as_Vsrno, 'S', 'D' )
		ls_Vndnm = f_Yeo_Vdnm_Pur( as_Vsrno, 'S', 'D' )
	Else
		ls_Vndr  = ""
		ls_Vndnm = ""
	End If
	
	// 불출부서 : 원재료 정상불출시 부서코드 spaces
	If ( (as_xuse = '01' or as_xuse = '04' or as_xuse = '07') and ( ls_cls = '10' or ls_cls = '50' ) ) Then
		ls_dept = ""
	else
		ls_dept = as_dept
	end if
	
	// 불출금액 : 이동평균단가 * 불출량(단, 외주가공품은 금액미관리)
	If (ls_cls = '10' and ls_srce <> '05') or ( ls_cls = '20' ) or ( ls_cls = '24' )  Then
		ll_amt = Truncate( (ld_cost * ad_qty), 0 ) 
	Else
		ll_amt = 0
	End If
		
	ds_trans_01.setitem(ll_row,"comltd",'01')                 // 회사
	ds_trans_01.setitem(ll_row,"sliptype",'IS')               // 전표구분
	ds_trans_01.setitem(ll_row,"srno",ls_srno)                // 전산번호(년월/Serial)
	ds_trans_01.setitem(ll_row,"srno1",as_xplant + as_div)    // 전산번호(지역 + 공장)
	ds_trans_01.setitem(ll_row,"srno2",ls_cls)                // 전산번호(계정)
	ds_trans_01.setitem(ll_row,"xplant",as_xplant)            // 지역
	ds_trans_01.setitem(ll_row,"div",as_div)                  // 공장
	ds_trans_01.setitem(ll_row,"costdiv",ls_cdiv)             // 원가사업장
	ds_trans_01.setitem(ll_row,"slno",ls_slno)                // 전표번호
	ds_trans_01.setitem(ll_row,"itno",as_itno)                // 품번
	ds_trans_01.setitem(ll_row,"itnm",ls_itnm)                // 품명
	ds_trans_01.setitem(ll_row,"spec",ls_spec)                // 규격
	ds_trans_01.setitem(ll_row,"cls",ls_cls)                  // 계정
	ds_trans_01.setitem(ll_row,"srce",ls_srce)                // 구입선
	ds_trans_01.setitem(ll_row,"xunit",ls_unit)               // 단위
	ds_trans_01.setitem(ll_row,"pdcd",ls_pdcd)                // 제품
	ds_trans_01.setitem(ll_row,"xtype",ls_type)               // item type
	ds_trans_01.setitem(ll_row,"xplan",ls_plan)               // 구매담당
	ds_trans_01.setitem(ll_row,"mlan",ls_maln)                // 자재담당
	ds_trans_01.setitem(ll_row,"kbcd",' ')                    // 간판여부
	ds_trans_01.setitem(ll_row,"vsrno",as_vsrno)              // 사급업체
	ds_trans_01.setitem(ll_row,"vndr",ls_vndr)                // 사업자번호
	ds_trans_01.setitem(ll_row,"vndnm",ls_vndnm)              // 업체명
	ds_trans_01.setitem(ll_row,"xpay",' ')                    // 결제조건
	ds_trans_01.setitem(ll_row,"dept",ls_dept)                // 불출부서
	ds_trans_01.setitem(ll_row,"tdte4",as_date)               // 불출일
	ds_trans_01.setitem(ll_row,'xuse',as_xuse)                // 불출용도
	ds_trans_01.setitem(ll_row,"tqty4",ad_qty)                // 불출량
	ds_trans_01.setitem(ll_row,"xcost",ld_cost)               // 이동평균단가
	ds_trans_01.setitem(ll_row,"tramt",ll_amt)                // 불출금액
	ds_trans_01.setitem(ll_row,"pjtno",as_pjtno)              // Project No
	ds_trans_01.setitem(ll_row,"invstcd",as_stcd)             // 재고상태
	ds_trans_01.setitem(ll_row,"tfill",as_srno)               // 여주공장 전산번호
	ds_trans_01.setitem(ll_row,"inptid",as_usrid)             // 입력자사번
	ds_trans_01.setitem(ll_row,"inptdt",as_inptdt)            // 입력일
	
Else
	
	DELETE FROM "PBINV"."INV401" 
	WHERE "COMLTD" = '01'       AND "SLIPTYPE" = :ls_sltype AND "SRNO" = :ls_dsrno AND 
	      "SRNO1"  = :ls_dsrno1 AND "SRNO2"    = :ls_dsrno2  using sqlca ;
	
End If

// 불출수량 계산
If ls_srce <> '03' Then
	If as_gubun = 'A' Then
		ld_outqty = ld_outqty + ad_qty
	Else
		ld_outqty = ld_outqty - ld_tqty4
	End If
End If

// 불출금액 계산
If (ls_srce = '01') or (ls_srce = '02') or &
	(ls_cls  = '10' and ls_srce = '04')  or (ls_cls  = '10' and ls_srce = '06') Then
	If as_gubun = 'A' Then
	 	ll_outamt = ll_outamt + ll_amt
	Else
		ll_outamt = ll_outamt - ll_tramt
	End If
End If

// 재고상태별 현재고수량 계산
// 사용가, 요수리, 폐품 < 0 일때 에러 체크
If ls_srce <> '03' Then
	
	// 사용가
	If as_stcd = 'U' then
		If as_gubun = 'A' Then
			ld_uqty = ld_uqty - ad_qty
			if ld_uqty < 0 then
				as_error = "품목재고(사용가) (-) 재고량 발생 "
				return -1
			End if
		Else
			ld_uqty = ld_uqty + ld_tqty4
		End If
	End If
	// 요수리
	If as_stcd = 'R' Then
		If as_gubun = 'A' Then
	    	ld_rqty = ld_rqty - ad_qty
			if ld_rqty < 0 then
				as_error = "품목재고(요수리) (-) 재고량 발생 "
				return -1
			End if
		Else
			ld_rqty = ld_rqty + ld_tqty4
		End If
	End If
	// 폐품
	If as_stcd = 'S' Then
		If as_gubun = 'A' Then
			ld_sqty = ld_sqty - ad_qty
			if ld_sqty < 0 then
				as_error = "품목재고(폐품) (-) 재고량 발생 "
				return -1
			End if
		Else
			ld_sqty = ld_sqty + ld_tqty4
		End If
	End If
	
	
	
	
	// 현재고 금액계산
	If as_gubun = 'A' Then
		ll_ohamt = ll_ohamt - ll_amt
	Else
		ll_ohamt = ll_ohamt + ll_tramt
	End If
	
End If
 
// 현재고수량 = 0 인데 현재고금액 > 0 면 불출누적금액 = 불출누적금액 + 현재고금액 & TRANS 불출금액 누적	   
If ls_srce <> '03' Then
	If as_gubun = 'A' Then
		If ( ld_uqty + ld_rqty + ld_sqty = 0 ) and ( ll_ohamt <> 0 ) Then
			ll_wamt  = ll_ohamt
			ll_ohamt = 0
			If (ls_cls = '10' and ls_srce <> '05') or ls_cls = '20' or ls_cls = '24'  Then
				ll_outamt = ll_outamt + ll_wamt
				ll_amt    = ll_amt + ll_wamt
				ds_trans_01.setitem(ll_row,"tramt",ll_amt)
			End If
		End If 
	End If
End If

If as_gubun = 'A' Then
	
	ds_trans_01.accepttext()
	
	If ds_trans_01.update() <> 1 Then
		rollback using sqlca;
		destroy ds_trans_01
		as_error = "Trans_DB update error"
		return -1		
	End If
	
End IF

If ls_srce <> '03' Then

	// 품목재고 Table Update
	UPDATE "PBINV"."INV101" 
		SET "OHUQTY" = :ld_uqty ,    "OHRQTY" = :ld_rqty,      "OHSQTY" = :ld_sqty,  "OHAMT"   = :ll_ohamt, 
			 "OUTQTY" = :ld_outqty,   "OUTAMT"  = :ll_outamt,   "UPDTID" = :as_usrid, "PBINV"."INV101"."ILUDT" = :as_date
	WHERE  "PBINV"."INV101"."COMLTD" = '01'    AND "PBINV"."INV101"."XPLANT" = :as_xplant AND
			 "PBINV"."INV101"."DIV"    = :as_div AND "PBINV"."INV101"."ITNO"   = :as_itno  using sqlca ;
	If sqlca.sqlcode <> 0  Then
		rollback using sqlca;
		as_error = "품목재고 Table Update error"
		return -1
	End If
 
End If

If as_gubun = 'A' Then
	commit using sqlca;
	destroy ds_trans_01
End If

return 0
	

end function

