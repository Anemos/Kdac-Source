$PBExportHeader$f_teamleader_abnormal_delete.srf
$PBExportComments$팀장 해임시 사용권한 강제 삭제(비정상적경우)
global type f_teamleader_abnormal_delete from function_object
end type

forward prototypes
global function boolean f_teamleader_abnormal_delete (string ag_empno, string ag_dept)
end prototypes

global function boolean f_teamleader_abnormal_delete (string ag_empno, string ag_dept);////////////////////////////////////////////////////////////////////
//////////팀장 해임시 권한 강제 삭제(비정상적 경우)//////////////
////////////////////////////////////////////////////////////////////
Long 		i,ln_server_count,ln_win_count,j
String  	ls_area, ls_window

datastore ds_1,ds_winid

sqlxx	=	Create transaction

ds_1	=	Create Datastore
ds_1.dataobject	=	'd_comm001_retrieve'
ds_1.settransobject(Sqlpis)

ds_winid	=	Create Datastore
ds_winid.dataobject = 'd_comm111_delete'
ds_winid.settransobject(Sqlca)

ln_server_count 	=	ds_1.retrieve()
ln_win_count 		= 	ds_winid.retrieve(ag_dept)

//해당부서에 해당하는 권한 없을때 바로 빠지게 수정-2012.01.06
if	ln_win_count	<	1	then
	destroy sqlxx 
	destroy ds_1
	destroy ds_winid
	return true
end if
//

for i = 1 to ln_server_count
	// remote autodown 관련
	ls_area 				= trim(ds_1.object.serverid[i])
	sqlxx.ServerName 	= trim(ds_1.object.servername[i])
	sqlxx.DBMS       	= trim(ds_1.object.serverdbms[i])
	sqlxx.Database   	= trim(ds_1.object.serverdatabase[i])
	sqlxx.LogId      	= trim(ds_1.object.serverlogid[i])
	sqlxx.LogPass    	= trim(ds_1.object.serverpassword[i])
	sqlxx.autocommit = true
	sqlxx.DBParm     = trim(ds_1.object.serverdbparm[i])

	connect using sqlxx;
	if sqlxx.sqlcode <> 0 then
		messagebox('경고',  ls_area + ' 서버가 연결되지 않습니다.')
		disconnect using sqlxx;
		destroy sqlxx 
		destroy ds_1
		destroy ds_winid
		return false
	end if
	
   for j = 1 to ln_win_count
		ls_window 	= 	trim(ds_winid.object.win_id[j])
		if 	sqlxx.dbms	=	"ODBC"	then
			delete from pbcommon.comm140
				where	 emp_no = :ag_empno and use_win = :ls_window 
			using sqlxx ;
		else
			delete from comm140
				where	 emp_no = :ag_empno and use_win = :ls_window 
			using sqlxx ;
		End if
		if 	sqlxx.sqlcode <> 0  then
			messagebox("확인",sqlxx.sqlerrtext)
			disconnect using sqlxx;
			destroy sqlxx 
			destroy ds_1
			destroy ds_winid
			return false
		End if
	next
	disconnect using sqlxx;
next

destroy sqlxx 
destroy ds_1
destroy ds_winid
return true


end function

