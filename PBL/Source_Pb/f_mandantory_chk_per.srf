$PBExportHeader$f_mandantory_chk_per.srf
$PBExportComments$필수항목 Check
global type f_mandantory_chk_per from function_object
end type

forward prototypes
global function integer f_mandantory_chk_per (datawindow ag_dw)
end prototypes

global function integer f_mandantory_chk_per (datawindow ag_dw);//////////////////////////////////////////////////////////////////////////
// * 기능강화(UPGRADE) 필수입력사항 Check 함수 *
//
// -> 칼럼의 배경색값에 조건(If문...)을 준 경우에도 Check...
//																		2002. 11. 3 박병주
//////////////////////////////////////////////////////////////////////////

ag_dw.AcceptText()

String 	ls_ColCnt
Long 		ll_RowCnt

ls_ColCnt = ag_dw.Object.DataWindow.Column.Count
ll_RowCnt = ag_dw.RowCount()

Long		ll_Row = 0, ll_i
String	ls_ColType, ls_ColName, ls_ColText, ls_Color_Syntax, ls_Color
Any		la_ColValue

Do While ll_Row <= ll_RowCnt
	ll_Row = ag_dw.GetNextModified(ll_Row, Primary!)
	If ll_Row = 0 Then Exit
	
	For ll_i = 1 To Dec(ls_ColCnt) 
		ls_ColType = Upper(ag_dw.Describe('#' + String(ll_i) + '.ColType'))
		ls_ColName = ag_dw.Describe('#' + String(ll_i) + '.Name')
		
		ls_Color_Syntax = ag_dw.Describe(ls_ColName + '.BackGround.Color')
		
		If Pos( ls_Color_Syntax, 'if', 1 ) = 0 Then
			ls_Color = ls_Color_Syntax
		Else				
			ls_Color_Syntax = Mid( ls_Color_Syntax, Pos( ls_Color_Syntax, 'if', 1 ) )
			ls_Color_Syntax = Left( ls_Color_Syntax, Len(ls_Color_Syntax) - 1 )			
			ls_Color = ag_dw.Describe( "Evaluate('" + ls_Color_Syntax +"', " + String(ll_Row) + ")" )		
		End If
		
		Choose Case Left(ls_ColType, 3)
			Case 'CHA'
				la_ColValue = Trim(ag_dw.GetItemString(ll_Row, ls_ColName))
				If Long( ls_Color ) = 31645917 Then
					If IsNull(la_ColValue) Or la_ColValue = "" Or la_ColValue = ' ' Then
						ls_ColText = ag_dw.Describe(ls_ColName + '_t.Text')
						MessageBox('확인!', ls_ColText + '는(은) 필수입력 항목입니다.', Exclamation!)
						ag_dw.SetRow(ll_Row)
						ag_dw.SetColumn(ll_i)
						ag_dw.SetFocus( )
						
						Return -1
					End If
				End If
				
			Case 'INT', 'DEC', 'LON', 'NUM', 'REA'
				
				la_ColValue = ag_dw.GetItemDecimal(ll_Row, ls_ColName)				
				If Long( ls_Color ) = 31645917 Then
					If IsNull(la_ColValue) Or la_ColValue <= 0 Then
						ls_ColText = ag_dw.Describe(ls_ColName + '_t.Text')
						
//						If la_ColValue < 0 Then
//							MessageBox('확인!', ls_ColText + '는(은) 음수값이 들어간 항목입니다.', Exclamation!)
//						Else
							MessageBox('확인!', ls_ColText + '는(은) 필수입력 항목입니다.', Exclamation!)
//						End If
						ag_dw.SetRow(ll_Row)
						ag_dw.SetColumn(ll_i)
						ag_dw.SetFocus( )
						
						Return -1
					End If
				End If
				
			Case 'DAT'
				la_ColValue = ag_dw.GetItemDateTime(ll_Row, ls_ColName)
				If Long( ls_Color ) = 31645917 Then
					If IsNull(la_ColValue) Then
						ls_ColText = ag_dw.Describe(ls_ColName + '_t.Text')
						MessageBox('확인!', ls_ColText + '는(은) 필수입력 항목입니다.', Exclamation!)
						ag_dw.SetRow(ll_Row)
						ag_dw.SetColumn(ll_i)
						ag_dw.SetFocus( )
						
						Return -1
					End If
				End If
				
		End Choose
		
	Next
	
Loop


Return 1
end function

