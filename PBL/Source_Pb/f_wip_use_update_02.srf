$PBExportHeader$f_wip_use_update_02.srf
$PBExportComments$재공 사용량 계산(WIP002)
global type f_wip_use_update_02 from function_object
end type

forward prototypes
global function decimal f_wip_use_update_02 (string a_comltd, string a_slipxtype, string a_srno, string a_srno1, string a_srno2, string a_xplant, string a_div, string a_slno, string a_itno, string a_pitno, string a_opcd, string a_xuse, string a_rtngub, string a_dept, string a_tdte4, decimal a_tqty4, decimal a_qty1, string a_gubun)
end prototypes

global function decimal f_wip_use_update_02 (string a_comltd, string a_slipxtype, string a_srno, string a_srno1, string a_srno2, string a_xplant, string a_div, string a_slno, string a_itno, string a_pitno, string a_opcd, string a_xuse, string a_rtngub, string a_dept, string a_tdte4, decimal a_tqty4, decimal a_qty1, string a_gubun);//***********************************************************************************
//
// 업체, 라인재공에  사용량 계산 UPDATE
// WIP004에 사용수량 UPDATE, WIP001 UPDATE
// 인수 : 회사, 입고전표, 구매요구번호, 분할발주횟수, 분할납품회수, 지역, 공장, 거래명세표번호,
//        하위품번, 입고품번, 옵션여부, 불출용도, 반납구분, 업체코드(부서), 입고일, 입고수량, 원단위량, 재공구분
// 반환값 : 남은 수량
//
//***********************************************************************************

decimal{1} l_n_tqty4
decimal{4} l_n_wdchqt,l_n_wbusqt1,l_n_wbusqt2,l_n_wbusqt3,l_n_wbusqt4,l_n_wbusqt5,l_n_wbusqt6,l_n_wbusqt7,&
			  l_n_wbusqt8, l_n_wbusqta,l_n_wbohqt,l_n_chkqty
decimal{5} l_n_wbavrg1,l_n_wbavrg2
decimal{0} l_n_wbusat1,l_n_wbusat2,l_n_wbusat3,l_n_wbusat4,l_n_wbusat5,l_n_wbusat6,l_n_wbusat7,&
			  l_n_wbusat8, l_n_wbusat9, l_n_wbusata,l_n_wbohat1,l_n_wbohat2
decimal{2} l_n_costav
string     l_s_plant,l_s_div,l_s_itno,l_s_opcd,l_s_wdslty,l_s_wdsrno,l_s_wdrvno,l_s_wddesc,l_s_wdspec,&
           l_s_wdusge,l_s_errchk,l_s_cls,l_s_srce,l_s_pdcd,l_s_xunit,l_s_orct, l_s_time, ls_adjdate, ls_rundate
string     ls_yy01, ls_mm01, ls_yy02, ls_mm02, l_s_prdpt

//마감월 가져오기
ls_rundate = mid(g_s_date,1,6)
ls_adjdate = uf_wip_addmonth(ls_rundate, -1)
ls_yy01 = mid(ls_adjdate,1,4)
ls_mm01 = mid(ls_adjdate,5,2)
ls_yy02 = mid(ls_rundate,1,4)
ls_mm02 = mid(ls_rundate,5,2)

//품명,규격, REVISNO NO
select itnm, spec, rvno into :l_s_wddesc,:l_s_wdspec,:l_s_wdrvno
	from pbinv.inv002
	where comltd = :g_s_company and itno = :a_itno
	using sqlca;

//계정, 구입선, 제품군, 재고단위
select cls,srce,pdcd,xunit,costav
	into:l_s_cls,:l_s_srce,:l_s_pdcd,:l_s_xunit,:l_n_costav
	from pbinv.inv402
	where  comltd = :g_s_company and xplant = :a_xplant and div = :a_div 
				and itno = :a_itno and xyear = :ls_adjdate
using sqlca ;

l_s_pdcd = mid(l_s_pdcd,1,2)
// a_opcd = '1'  => a_qty1 : 원단위량, l_n_qty4 : 자재수량
// a_opcd = '2'  => a_qty1 : 재공사용량
if f_spacechk(a_opcd) = -1 then
	l_n_tqty4 = a_tqty4               	//자재수량
	l_n_wdchqt = a_qty1 * l_n_tqty4		//원단위량 * 자재수량
else 
	if a_opcd = '1' then
		l_n_tqty4 = a_tqty4               	//자재수량
		l_n_wdchqt = a_qty1 * l_n_tqty4		//원단위량 * 자재수량
	else
		l_n_tqty4 = a_tqty4
		l_n_wdchqt = a_qty1
	end if
end if

// line,업체 사용품번의 재공구분 및 구입선에 따라서 업데이트 유무 결정
if a_gubun = '1' then
	if f_spacechk(l_s_srce) = -1 or trim(l_s_srce) = '03' then
		return 0
	end if
elseif a_gubun = '2' then
	if f_spacechk(l_s_srce) = -1 and a_opcd = '2' then
		return 0
	end if
end if

// line,업체 재공 구분에 따라서 Work Center 변경
if a_gubun = '1' then
	l_s_orct = '9999'
	l_s_prdpt = trim(a_dept)
elseif a_gubun = '2' then
	l_s_orct = trim(a_dept)
	l_s_prdpt = '9999'
end if

SELECT WBAVRG1,WBAVRG2,WBUSQT1,WBUSQT2,WBUSQT3,WBUSQT4,WBUSQT5,WBUSQT6,WBUSQT7,WBUSQT8,WBUSQTA INTO
		:l_n_wbavrg1,:l_n_wbavrg2,:l_n_wbusqt1,:l_n_wbusqt2,:l_n_wbusqt3,:l_n_wbusqt4,:l_n_wbusqt5,
		:l_n_wbusqt6,:l_n_wbusqt7,:l_n_wbusqt8,:l_n_wbusqta   FROM "PBWIP"."WIP002" 
	WHERE WBCMCD = :G_S_COMPANY AND WBPLANT = :a_xplant AND WBDVSN = :a_div AND 
			WBORCT = :l_s_orct AND WBITNO = :a_itno AND WBYEAR||WBMONTH = :ls_adjdate 
USING SQLCA;

if sqlca.sqlcode <> 0 then
	l_n_wbavrg1 = l_n_costav
	l_n_wbavrg2 = 0
	l_n_wbusqt1 = 0 
	l_n_wbusqt2 = 0 
	l_n_wbusqt3 = 0 
	l_n_wbusqt4 = 0 
	l_n_wbusqt5 = 0 
	l_n_wbusqt6 = 0
	l_n_wbusqt7 = 0
	l_n_wbusqt8 = 0 
	l_n_wbusqta  = 0
	l_n_wbohqt = 0
	l_n_wbusat1 = 0 
	l_n_wbusat2 = 0 
	l_n_wbusat3 = 0 
	l_n_wbusat4 = 0 
	l_n_wbusat5 = 0 
	l_n_wbusat6 = 0
	l_n_wbusat7 = 0
	l_n_wbusat8 = 0 
	l_n_wbusat9 = 0
	l_n_wbusata = 0
	l_s_errchk  = '1'
else
	if l_n_wbavrg1 = 0 then
		l_n_wbavrg1 = l_n_costav
	end if
	
	SELECT "PBWIP"."WIP002"."WBBGQT"  
   	INTO :l_n_wbohqt  
   	FROM "PBWIP"."WIP002"  
   	WHERE ( "PBWIP"."WIP002"."WBCMCD" = :g_s_company ) AND  
         	( "PBWIP"."WIP002"."WBPLANT" = :a_xplant ) AND  
         	( "PBWIP"."WIP002"."WBDVSN" = :a_div ) AND  
         	( "PBWIP"."WIP002"."WBORCT" = :l_s_orct ) AND  
         	( "PBWIP"."WIP002"."WBITNO" = :a_itno ) AND  
         	( "PBWIP"."WIP002"."WBYEAR"||"PBWIP"."WIP002"."WBMONTH" = :ls_rundate )   
				using sqlca;
end if

//전표 성격에 따라서 용도 및 사용 수량 변경
if a_gubun = '2' then
	Choose Case a_slipxtype
		Case 'RP'
			//사급품입고 ( 업체 -> 사급완성품창고 )
			l_s_wdusge = '02'
			l_n_wbusqt2 += l_n_wdchqt
	End Choose
else
	Choose Case a_slipxtype
		Case 'RS'
			if a_xuse <> '04' then
				//라인사용 ( 라인 -> 라인 )
				l_s_wdusge = '07'
				l_n_wbusqt7 += l_n_wdchqt
			else
				//라인사용 ( 10/03 업체 -> 라인 )
				l_n_wdchqt = - ( l_n_wdchqt )
				l_s_wdusge = '02'
				l_n_wbusqt2 += l_n_wdchqt
			end if
		Case 'IS'
			if a_xuse = '07' then
				//유상반출 ( 10/03 라인 -> 업체 )
				l_s_wdusge = '03'
				l_n_wbusqt3 += l_n_wdchqt
			elseif a_xuse = '04' then
				//무상반출 ( 10/03 라인 -> 업체 )
				l_s_wdusge = '02'
				l_n_wbusqt2 += l_n_wdchqt
			else
				return 0
			end if
		Case 'RM'
			//라인 -> 완제품창고 ( 30, 10/03 )
			l_s_wdusge = '01'
			l_n_wbusqt1 += l_n_wdchqt
		Case 'SM'
			//완제품창고 -> 라인 ( 30, 10/03 )
			l_n_wdchqt = - ( l_n_wdchqt )
			l_s_wdusge = '01'
			l_n_wbusqt1 += l_n_wdchqt
		Case Else
			return 0
	End Choose	
end if
//현재공 계산루틴
if l_n_wbohqt < l_n_wdchqt and a_opcd = '1' and l_s_srce <> '04' then
	//호환주품번이면서 현재공이 사용량보다 적은 경우 => 예외 : 구입선이 04인품번과 10/03이 호환이 걸린경우
	if l_n_wbohqt <= 0 then
		return l_n_wdchqt
	end if
	l_n_chkqty = l_n_wdchqt - l_n_wbohqt
	l_n_wbohqt = 0
else
	l_n_wbohqt -= l_n_wdchqt 
end if

l_n_wbusat1 = l_n_wbusqt1 * l_n_wbavrg1 
l_n_wbusat2 = l_n_wbusqt2 * l_n_wbavrg1 
l_n_wbusat3 = l_n_wbusqt3 * l_n_wbavrg1  
l_n_wbusat4 = l_n_wbusqt4 * l_n_wbavrg1  
l_n_wbusat5 = l_n_wbusqt5 * l_n_wbavrg1 
l_n_wbusat6 = l_n_wbusqt6 * l_n_wbavrg1 
l_n_wbusat7 = l_n_wbusqt7 * l_n_wbavrg1 
l_n_wbusat8 = l_n_wbusqt8 * l_n_wbavrg1
l_n_wbusata = l_n_wbusqta * l_n_wbavrg1
l_n_wbohat1 = l_n_wbohqt  * l_n_wbavrg1 
l_n_wbohat2 = 0 

// 재공 Trans insert
l_s_wdsrno    = f_wip_get_serialno(g_s_company)  

if len(l_s_wdsrno) = 1 then
	return 0
end if
l_s_time = g_s_datetime
INSERT INTO "PBWIP"."WIP004" VALUES ( 
	:a_comltd,'WC',:l_s_wdsrno,:a_xplant,:a_div,:a_gubun,:a_itno,:l_s_wdrvno,:l_s_wddesc,:l_s_wdspec,
	:l_s_xunit,:l_s_cls,:l_s_srce,:l_s_wdusge,:l_s_pdcd,:a_slno,:a_slipxtype,:a_srno,:a_srno1,:a_srno2,
	:a_pitno,:l_s_prdpt,:l_s_orct,:a_tdte4,:l_n_tqty4,:l_n_wdchqt,:g_s_ipaddr,:g_s_macaddr,:g_s_empno,:g_s_empno,
	:g_s_date,:l_s_time,:g_s_date
	)
using sqlca ;

if sqlca.sqlcode <> 0 then
	return 0
end if
// 재공 이월 에 존재하면 Update, else Insert
if l_s_errchk = '1' then
	//마감월 입력
	 INSERT INTO "PBWIP"."WIP002"  
         ( "WBCMCD","WBPLANT","WBDVSN","WBORCT","WBITNO","WBYEAR","WBMONTH","WBREV","WBIOCD",   
           "WBITCL","WBSRCE","WBPDCD","WBUNIT","WBTYPE","WBDESC","WBSPEC","WBSCRP","WBRETN",   
           "WBAVRG1","WBAVRG2","WBBGQT","WBBGAT1","WBBGAT2","WBINQT","WBINAT1","WBINAT2","WBINAT3","WBINAT4",   
           "WBUSQT1","WBUSAT1","WBUSQT2","WBUSAT2","WBUSQT3","WBUSAT3","WBUSQT4","WBUSAT4","WBUSQT5","WBUSAT5",   
           "WBUSQT6","WBUSAT6","WBUSQT7","WBUSAT7","WBUSQT8","WBUSAT8","WBUSAT9","WBUSQTA","WBUSATA",   
           "WBPLAN","WBIPADDR","WBMACADDR","WBINPTDT","WBUPDTDT" )  
  	 SELECT  comltd, xplant, div,:l_s_orct, itno,:ls_yy01,:ls_mm01, rvno, :a_gubun,
				cls,  srce, substr(pdcd,1,2), xunit, xtype, itnm, spec, 0, 0,
				:l_n_wbavrg1,   0,      0,        0,         0,       0,       0,        0,       0,       0,
				:l_n_wbusqt1,:l_n_wbusat1,:l_n_wbusqt2,:l_n_wbusat2,:l_n_wbusqt3,:l_n_wbusat3,:l_n_wbusqt4,:l_n_wbusat4,:l_n_wbusqt5,:l_n_wbusat5,
		      :l_n_wbusqt6,:l_n_wbusat6,:l_n_wbusqt7,:l_n_wbusat7,:l_n_wbusqt8,:l_n_wbusat8,:l_n_wbusat9,:l_n_wbusqta,:l_n_wbusata,
				' ', :g_s_ipaddr, :g_s_macaddr, :g_s_date, :g_s_date
		FROM "PBINV"."INV402"
		WHERE "PBINV"."INV402"."COMLTD" = :g_s_company AND
				"PBINV"."INV402"."XPLANT" = :a_xplant AND
				"PBINV"."INV402"."DIV" = :a_div AND
				"PBINV"."INV402"."XYEAR" = :ls_adjdate AND
				"PBINV"."INV402"."ITNO" = :a_itno 
				using sqlca;
	if sqlca.sqlcode <> 0 then
		return 0
	end if
	//현재월 입력
	INSERT INTO "PBWIP"."WIP002"  
         ( "WBCMCD","WBPLANT","WBDVSN","WBORCT","WBITNO","WBYEAR","WBMONTH","WBREV","WBIOCD",   
           "WBITCL","WBSRCE","WBPDCD","WBUNIT","WBTYPE","WBDESC","WBSPEC","WBSCRP","WBRETN",   
           "WBAVRG1","WBAVRG2","WBBGQT","WBBGAT1","WBBGAT2","WBINQT","WBINAT1","WBINAT2","WBINAT3","WBINAT4",   
           "WBUSQT1","WBUSAT1","WBUSQT2","WBUSAT2","WBUSQT3","WBUSAT3","WBUSQT4","WBUSAT4","WBUSQT5","WBUSAT5",   
           "WBUSQT6","WBUSAT6","WBUSQT7","WBUSAT7","WBUSQT8","WBUSAT8","WBUSAT9","WBUSQTA","WBUSATA",   
           "WBPLAN","WBIPADDR","WBMACADDR","WBINPTDT","WBUPDTDT" )  
  	 SELECT  comltd, xplant, div,:l_s_orct, itno,:ls_yy02,:ls_mm02, rvno, :a_gubun,
				cls,  srce, substr(pdcd,1,2), xunit, xtype, itnm, spec, 0, 0,
				:l_n_wbavrg1,   0,:l_n_wbohqt,:l_n_wbohat1,         0,       0,       0,        0,       0,       0,
				0,		0,		0,		0,		0,		0,		0,		0,		0,		0,
		      0,		0,		0,		0,		0,		0,		0,		0,		0,
				' ', :g_s_ipaddr, :g_s_macaddr, :g_s_date, :g_s_date
		FROM "PBINV"."INV402"
		WHERE "PBINV"."INV402"."COMLTD" = :g_s_company AND
				"PBINV"."INV402"."XPLANT" = :a_xplant AND
				"PBINV"."INV402"."DIV" = :a_div AND
				"PBINV"."INV402"."XYEAR" = :ls_adjdate AND
				"PBINV"."INV402"."ITNO" = :a_itno 
				using sqlca;
else
	//마감월 UPDATE
	update "PBWIP"."WIP002"
		set WBUSQT1 = :l_n_wbusqt1,WBUSAT1 = :l_n_wbusat1,WBUSQT2 = :l_n_wbusqt2,WBUSAT2 = :l_n_wbusat2,
			 WBUSQT3 = :l_n_wbusqt3,WBUSAT3 = :l_n_wbusat3,WBUSQT4 = :l_n_wbusqt4,WBUSAT4 = :l_n_wbusat4,
			 WBUSQT5 = :l_n_wbusqt5,WBUSAT5 = :l_n_wbusat5,WBUSQT6 = :l_n_wbusqt6,WBUSAT6 = :l_n_wbusat6,
			 WBUSQT7 = :l_n_wbusqt7,WBUSAT7 = :l_n_wbusat7,WBUSQT8 = :l_n_wbusqt8,WBUSAT8 = :l_n_wbusat8,
			 WBUSAT9 = :l_n_wbusat9,WBUSQTA = :l_n_wbusqta,WBUSATA = :l_n_wbusata
	where WBCMCD = :G_S_COMPANY AND WBPLANT = :a_xplant AND WBDVSN = :a_div AND 
			WBORCT = :l_s_orct AND WBITNO = :a_itno AND WBYEAR||WBMONTH = :ls_adjdate
	using sqlca;
	if sqlca.sqlcode <> 0 then
		return 0
	end if
	//현재공 UPDATE
	update "PBWIP"."WIP002"
		set WBBGQT = :l_n_wbohqt,WBBGAT1 = :l_n_wbohat1
	where WBCMCD = :G_S_COMPANY AND WBPLANT = :a_xplant AND WBDVSN = :a_div AND 
			WBORCT = :l_s_orct AND WBITNO = :a_itno AND WBYEAR||WBMONTH = :ls_rundate
	using sqlca;
	if sqlca.sqlcode <> 0 then
		return 0
	end if
end if
if l_n_chkqty > 0 then
	return l_n_chkqty
else
	return 0
end if

end function

