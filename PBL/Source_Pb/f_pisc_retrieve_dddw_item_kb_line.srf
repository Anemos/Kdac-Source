$PBExportHeader$f_pisc_retrieve_dddw_item_kb_line.srf
$PBExportComments$dddw을 조회하는 함수 - 제품 (간판마스터에서 모델군에 속한 제품 조회)
global type f_pisc_retrieve_dddw_item_kb_line from function_object
end type

forward prototypes
global subroutine f_pisc_retrieve_dddw_item_kb_line (datawindow fdw_1, string fs_areacode, string fs_divisioncode, string fs_workcenter, string fs_linecode, string fs_itemcode, boolean fb_allflag, ref string rs_itemcode, ref string rs_itemname)
end prototypes

global subroutine f_pisc_retrieve_dddw_item_kb_line (datawindow fdw_1, string fs_areacode, string fs_divisioncode, string fs_workcenter, string fs_linecode, string fs_itemcode, boolean fb_allflag, ref string rs_itemcode, ref string rs_itemname);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		:	f_pisc_retrieve_dddw_item_kb_line
//	Access		:	public
//	Arguments	:	DataWindow		fdw_1						조회하고자 하는 DDDW Object
//						string			fs_areacode				조회하고자 하는 지역
//						string			fs_divisioncode		조회하고자 하는 공장 코드
//						string			fs_workcenter			조회하고자 하는 조 코드
//						string			fs_linecode				조회하고자 하는 라인 코드
//						string			fs_itemcode				조회하고자 하는 품번 (일반적으로 '%' 을 사용하도록)
//						boolean			fb_allflag				조회된 모델군 정보가 2개 이상의 Record 일 경우
//																		True : '전체' 항목 삽입 (모델군코드는 '%', 모델군명은 '전체')
//																		False : '전체' 항목 미 삽입
//						string			rs_itemcode			선택된 품번 (reference)
//						string			rs_itemname				선택된 품명 (reference)
//	Returns		: none
//	Description	: 제품을 선택하기 위한 DDDW 을 조회하기 위하여
//						간판마스터에서 모델군에 속한 제품만 조회
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Kim Jin-Su
// Coded Date	: 2002.09.04
///////////////////////////////////////////////////////////////////////////////////////////////////////////
Long ll_rowcount
Datawindowchild ldwc_1

//fdw_1.InsertRow(0)

If fdw_1.GetChild('DDDWCode', ldwc_1) = 1 Then
	ldwc_1.SetTransObject(sqlpis)
	ldwc_1.Retrieve(fs_areacode, fs_divisioncode, fs_workcenter, fs_linecode, fs_itemcode)
	ll_rowcount = ldwc_1.RowCount()
	If ll_rowcount < 1 Then
		fdw_1.ReSet()
		fdw_1.InsertRow(0)
		rs_itemcode = ''
		rs_itemname = ''
	ElseIf ll_rowcount	= 1 Then
		rs_itemcode		= Trim(ldwc_1.GetItemString(1, 'itemcode'))
		rs_itemname	= Trim(ldwc_1.GetItemString(1, 'itemname'))
		fdw_1.Setitem(1, 'DDDWCode', rs_itemcode)
		f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'modelid', 30)
	ElseIf ll_rowcount > 1 Then
		If fb_allflag = False Then
			rs_itemcode		= Trim(ldwc_1.GetItemString(1, 'itemcode'))
			rs_itemname	= Trim(ldwc_1.GetItemString(1, 'itemname'))
			fdw_1.Setitem(1, 'DDDWCode', rs_itemcode)
			f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'modelid', 30)
		Else
			fdw_1.GetChild('DDDWCode', ldwc_1)
			ldwc_1.InsertRow(1)
			ldwc_1.Setitem(1, 'itemcode', 'ALL')
			ldwc_1.Setitem(1, 'itemname', '전체')
			ldwc_1.Setitem(1, 'DisplayName', '전체')
			rs_itemcode = '%'
			rs_itemname = '전체'
			fdw_1.Setitem(1, 'DDDWCode', 'ALL')
			f_pisc_set_dddw_width(fdw_1, 'DDDWCode', ldwc_1, 'modelid', 30)
		End If
	End If
End If
end subroutine

