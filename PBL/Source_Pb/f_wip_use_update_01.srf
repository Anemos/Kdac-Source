$PBExportHeader$f_wip_use_update_01.srf
$PBExportComments$재공 사용량 계산(WIP001)
global type f_wip_use_update_01 from function_object
end type

forward prototypes
global function decimal f_wip_use_update_01 (string a_comltd, string a_slipxtype, string a_srno, string a_srno1, string a_srno2, string a_xplant, string a_div, string a_slno, string a_itno, string a_pitno, string a_opcd, string a_xuse, string a_rtngub, string a_dept, string a_tdte4, decimal a_tqty4, decimal a_qty1, string a_gubun)
end prototypes

global function decimal f_wip_use_update_01 (string a_comltd, string a_slipxtype, string a_srno, string a_srno1, string a_srno2, string a_xplant, string a_div, string a_slno, string a_itno, string a_pitno, string a_opcd, string a_xuse, string a_rtngub, string a_dept, string a_tdte4, decimal a_tqty4, decimal a_qty1, string a_gubun);//***********************************************************************************
//
// 업체, 라인재공에  사용량 계산 UPDATE
// WIP004에 사용수량 UPDATE, WIP001 UPDATE
// 인수 : 회사, 입고전표, 구매요구번호, 분할발주횟수, 분할납품회수, 지역, 공장, 거래명세표번호,
//        하위품번, 입고품번, 옵션여부, 불출용도, 반납구분, 업체코드(부서), 입고일, 입고수량, 원단위량, 재공구분
// 반환값 : 남은 수량
//
//***********************************************************************************

decimal{1} l_n_tqty4
decimal{4} l_n_wdchqt,l_n_wausqt1,l_n_wausqt2,l_n_wausqt3,l_n_wausqt4,l_n_wausqt5,l_n_wausqt6,l_n_wausqt7,&
			  l_n_wausqt8,l_n_waohqt,l_n_chkqty
decimal{5} l_n_waavrg1,l_n_waavrg2
decimal{0} l_n_wausat1,l_n_wausat2,l_n_wausat3,l_n_wausat4,l_n_wausat5,l_n_wausat6,l_n_wausat7,&
			  l_n_wausat8,l_n_waohat1,l_n_waohat2
decimal{2} l_n_costav
string     l_s_plant,l_s_div,l_s_itno,l_s_opcd,l_s_wdslty,l_s_wdsrno,l_s_wdrvno,l_s_wddesc,l_s_wdspec,&
           l_s_wdusge,l_s_errchk,l_s_cls,l_s_srce,l_s_pdcd,l_s_xunit,l_s_orct, l_s_time, l_s_prdpt

//품명,규격, REVISNO NO
select itnm, spec, rvno into :l_s_wddesc,:l_s_wdspec,:l_s_wdrvno
	from pbinv.inv002
	where comltd = :g_s_company and itno = :a_itno
	using sqlca;

//계정, 구입선, 제품군, 재고단위
select cls,srce,pdcd,xunit,costav
	into:l_s_cls,:l_s_srce,:l_s_pdcd,:l_s_xunit,:l_n_costav
	from pbinv.inv101
	where  comltd = :g_s_company and xplant = :a_xplant and div = :a_div and itno = :a_itno
using sqlca ;

l_s_pdcd = mid(l_s_pdcd,1,2)
// a_opcd = '1'  => a_qty1 : 원단위량, l_n_qty4 : 자재수량
// a_opcd = '2'  => a_qty1 : 재공사용량
if f_spacechk(a_opcd) = -1 then
	l_n_tqty4 = a_tqty4   	//자재수량
	l_n_wdchqt = a_qty1 * l_n_tqty4			//원단위량 * 자재수량
else 
	if a_opcd = '1' then
		l_n_tqty4 = a_tqty4    //자재수량
		l_n_wdchqt = a_qty1 * l_n_tqty4		//원단위량 * 자재수량
	else
		l_n_tqty4 = a_tqty4
		l_n_wdchqt = a_qty1
	end if
end if

// line,업체 사용품번의 재공구분 및 구입선에 따라서 업데이트 유무 결정
if a_gubun = '1' then
	if trim(l_s_srce) = ' ' or trim(l_s_srce) = '03' then
		return 0
	end if
elseif a_gubun = '2' then
	if ( trim(l_s_srce) = ' ') and a_opcd = '2' then
		return 0
	end if
end if

// line,업체 재공 구분에 따라서 Work Center 변경
if a_gubun = '1' then
	l_s_orct = '9999'
	l_s_prdpt = trim(a_dept)
elseif a_gubun = '2' then
	l_s_orct = trim(a_dept)
	l_s_prdpt = '9999'
end if

SELECT WAAVRG1,WAAVRG2,WAUSQT1,WAUSQT2,WAUSQT3,WAUSQT4,WAUSQT5,WAUSQT6,WAUSQT7,WAUSQT8,WAOHQT INTO
		:l_n_waavrg1,:l_n_waavrg2,:l_n_wausqt1,:l_n_wausqt2,:l_n_wausqt3,:l_n_wausqt4,:l_n_wausqt5,
		:l_n_wausqt6,:l_n_wausqt7,:l_n_wausqt8,:l_n_waohqt   FROM "PBWIP"."WIP001" 
	WHERE WACMCD = :G_S_COMPANY AND WAPLANT = :a_xplant AND WADVSN = :a_div AND 
			WAORCT = :l_s_orct AND WAITNO = :a_itno 
USING SQLCA;

if sqlca.sqlcode <> 0 then
	l_n_waavrg1 = l_n_costav
	l_n_waavrg2 = 0
	l_n_wausqt1 = 0 
	l_n_wausqt2 = 0 
	l_n_wausqt3 = 0 
	l_n_wausqt4 = 0 
	l_n_wausqt5 = 0 
	l_n_wausqt6 = 0
	l_n_wausqt7 = 0
	l_n_wausqt8 = 0 
	l_n_waohqt  = 0
	l_n_wausat1 = 0 
	l_n_wausat2 = 0 
	l_n_wausat3 = 0 
	l_n_wausat4 = 0 
	l_n_wausat5 = 0 
	l_n_wausat6 = 0
	l_n_wausat7 = 0
	l_n_wausat8 = 0 
	l_n_waohat1 = 0
	l_n_waohat2 = 0
	l_s_errchk  = '1'
else
	if l_n_waavrg1 = 0 then
		l_n_waavrg1 = l_n_costav
	end if
end if

//전표 성격에 따라서 용도 및 사용 수량 변경
if a_gubun = '2' then
	if a_slipxtype = 'RP' then
			//사급품입고 ( 업체 -> 사급완성품창고 )
			l_s_wdusge = '02'
			l_n_wausqt2 += l_n_wdchqt
	end if
else
	Choose Case a_slipxtype
		Case 'RS'
			if a_xuse <> '04' then
				//라인사용 ( 라인 -> 라인 )
				l_s_wdusge = '07'
				l_n_wausqt7 += l_n_wdchqt
			else
				//라인사용 ( 10/03 업체 -> 라인 )
				l_n_wdchqt = - ( l_n_wdchqt )
				l_s_wdusge = '02'
				l_n_wausqt2 += l_n_wdchqt
			end if
		Case 'IS'
			if a_xuse = '07' then
				//유상반출 ( 10/03 라인 -> 업체 )
				l_s_wdusge = '03'
				l_n_wausqt3 += l_n_wdchqt
			elseif a_xuse = '04' then
				//무상반출 ( 10/03 라인 -> 업체 )
				l_s_wdusge = '02'
				l_n_wausqt2 += l_n_wdchqt
			else
				return 0
			end if
		Case 'RM'
			//라인 -> 완제품창고 ( 30, 10/03 )
			l_s_wdusge = '01'
			l_n_wausqt1 += l_n_wdchqt
		Case 'SM'
			//완제품창고 -> 라인 ( 30, 10/03 )
			l_n_wdchqt = - ( l_n_wdchqt )
			l_s_wdusge = '01'
			l_n_wausqt1 += l_n_wdchqt
		Case Else
			return 0
	End Choose	
end if
//현재공 계산루틴
if l_n_waohqt < l_n_wdchqt and a_opcd = '1' and l_s_srce <> '04' then
	//호환주품번이면서 현재공이 사용량보다 적은 경우
	if l_n_waohqt <= 0 then
		return l_n_wdchqt
	end if
	l_n_chkqty = l_n_wdchqt - l_n_waohqt
	l_n_wdchqt = l_n_waohqt
	l_n_waohqt = 0
else
	l_n_waohqt -= l_n_wdchqt 
end if

l_n_wausat1 = l_n_wausqt1 * l_n_waavrg1 
l_n_wausat2 = l_n_wausqt2 * l_n_waavrg1 
l_n_wausat3 = l_n_wausqt3 * l_n_waavrg1  
l_n_wausat4 = l_n_wausqt4 * l_n_waavrg1  
l_n_wausat5 = l_n_wausqt5 * l_n_waavrg1 
l_n_wausat6 = l_n_wausqt6 * l_n_waavrg1 
l_n_wausat7 = l_n_wausqt7 * l_n_waavrg1 
l_n_wausat8 = l_n_wausqt8 * l_n_waavrg1 
l_n_waohat1 = l_n_waohqt  * l_n_waavrg1 
l_n_waohat2 = l_n_waohqt  * l_n_waavrg2 

// 재공 Trans insert
l_s_wdsrno    = f_wip_get_serialno(g_s_company)  

if len(l_s_wdsrno) = 1 then
	return 0
end if
l_s_time = g_s_datetime
INSERT INTO "PBWIP"."WIP004" VALUES ( 
	:a_comltd,'WC',:l_s_wdsrno,:a_xplant,:a_div,:a_gubun,:a_itno,:l_s_wdrvno,:l_s_wddesc,:l_s_wdspec,
	:l_s_xunit,:l_s_cls,:l_s_srce,:l_s_wdusge,:l_s_pdcd,:a_slno,:a_slipxtype,:a_srno,:a_srno1,:a_srno2,
	:a_pitno,:l_s_prdpt,:l_s_orct,:a_tdte4,:l_n_tqty4,:l_n_wdchqt,:g_s_ipaddr,:g_s_macaddr,:g_s_empno,:g_s_empno,
	:g_s_date,:l_s_time,:g_s_date
	)
using sqlca ;

if sqlca.sqlcode <> 0 then
	return 0
end if
// 재공 Balance 에 존재하면 Update, else Insert
if l_s_errchk = '1' then
	INSERT INTO "PBWIP"."WIP001" VALUES ( 
		:g_s_company,:a_xplant,:a_div,:l_s_orct,:a_itno,:a_gubun,:l_n_waavrg1,0,0,0,0,0,0,0,0,0,
		:l_n_wausqt1,:l_n_wausat1,:l_n_wausqt2,:l_n_wausat2,:l_n_wausqt3,:l_n_wausat3,
		:l_n_wausqt4,:l_n_wausat4,:l_n_wausqt5,:l_n_wausat5,:l_n_wausqt6,:l_n_wausat6,
		:l_n_wausqt7,:l_n_wausat7,:l_n_wausqt8,:l_n_wausat8,0,
		:l_n_waohqt,:l_n_waohat1,:l_n_waohat2,0,0,' ',:g_s_ipaddr,:g_s_macaddr,:g_s_date,:g_s_date 
		)
	using sqlca ;
	if sqlca.sqlcode <> 0 then
		return 0
	end if
else
	update "PBWIP"."WIP001"
		set WAUSQT1 = :l_n_wausqt1,WAUSAT1 = :l_n_wausat1,WAUSQT2 = :l_n_wausqt2,WAUSAT2 = :l_n_wausat2,
			 WAUSQT3 = :l_n_wausqt3,WAUSAT3 = :l_n_wausat3,WAUSQT4 = :l_n_wausqt4,WAUSAT4 = :l_n_wausat4,
			 WAUSQT5 = :l_n_wausqt5,WAUSAT5 = :l_n_wausat5,WAUSQT6 = :l_n_wausqt6,WAUSAT6 = :l_n_wausat6,
			 WAUSQT7 = :l_n_wausqt7,WAUSAT7 = :l_n_wausat7,WAUSQT8 = :l_n_wausqt8,WAUSAT8 = :l_n_wausat8,
			 WAOHQT  = :l_n_waohqt, WAOHAT1 = :l_n_waohat1, WAOHAT2 = :l_n_waohat2
	where WACMCD = :G_S_COMPANY AND WAPLANT = :a_xplant AND WADVSN = :a_div AND 
			WAORCT = :l_s_orct AND WAITNO = :a_itno
	using sqlca;
	if sqlca.sqlcode <> 0 then
		return 0
	end if
end if
if l_n_chkqty > 0 then
	return l_n_chkqty
else
	return 0
end if

end function

