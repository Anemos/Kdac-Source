$PBExportHeader$f_null_kill.srf
$PBExportComments$저장할 DataWindow NULL 제거 Global Function
global type f_null_kill from function_object
end type

forward prototypes
global subroutine f_null_kill (datawindow ag_dw)
end prototypes

global subroutine f_null_kill (datawindow ag_dw);///////////////////////////////////////////////////
// 저장할려고 하는 DataWindow의 Null값을 제거
//
// Column(문자):   Null -> ' '(Space)
// Column(숫자):   Null -> 0
///////////////////////////////////////////////////


String ls_ColCnt
Long	 ll_RowCnt

ls_ColCnt = ag_dw.Object.DataWindow.Column.Count
ll_RowCnt = ag_dw.RowCount()

String ls_ColType
Any	 la_ColValue
Long	 ll_Row = 0, ll_i

Do While ll_Row <= ll_RowCnt
	ll_Row = ag_dw.GetNextModified(ll_Row, Primary!)
	If ll_Row = 0 Then Exit
	
	If ag_dw.GetItemStatus( ll_Row, 0, Primary! ) = DataModified! Then Continue
	
	For ll_i = 1 To Dec(ls_ColCnt)
		ls_ColType = Left(ag_dw.Describe('#' + String(ll_i) + '.ColType'), 3)
		
		Choose Case Upper(ls_ColType)
			Case 'CHA'
				la_ColValue = ag_dw.GetItemString(ll_Row, ll_i)
				If IsNull(la_ColValue) Or la_ColValue = "" Then
					ag_dw.SetItem(ll_Row, ll_i, ' ')  // NULL -> Space
				End If
			Case 'DEC', 'NUM'
				la_ColValue = ag_dw.GetItemNumber(ll_Row, ll_i)
				If IsNull(la_ColValue) Then
					ag_dw.SetItem(ll_Row, ll_i, 0) // NULL -> 0
				End If
		End Choose
	Next	
Loop
end subroutine

