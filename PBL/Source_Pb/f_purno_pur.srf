$PBExportHeader$f_purno_pur.srf
$PBExportComments$발주번호부여(담당,업체,구분,지역,공장,구매용도)
global type f_purno_pur from function_object
end type

forward prototypes
global function string f_purno_pur (string ag_s_xplan, string ag_s_vsrno, string ag_s_gubun, string ag_s_xplant, string ag_s_div, string ag_s_xuse)
end prototypes

global function string f_purno_pur (string ag_s_xplan, string ag_s_vsrno, string ag_s_gubun, string ag_s_xplant, string ag_s_div, string ag_s_xuse);////////////////////////////////////////////////////////////////////
// 발주번호부여
//          argument : 구매담당자
//                     업체전산번호
//                     구분 (정상 - "1" ,취소 - "2" , "N"-발주 HEAD 생성 안함.)
//                     지역
//							  공장
//                     구매용도
//
//          적용 : w_pur412u(종전가 자동발주)                     
////////////////////////////////////////////////////////////////////
String ls_purno, ls_srno, ls_xyear, ls_ckey, ls_tod ,ls_pgubu
long   ln_ctlno
//
ls_ckey = "PURNO" + g_s_deptcd

//자재 Control DB
SELECT "PBINV"."INV403"."SRNO", "PBINV"."INV403"."XYEAR"
  INTO :ln_ctlno,       :ls_xyear
FROM   "PBINV"."INV403"
WHERE "PBINV"."INV403"."COMLTD" = '01'  
 AND "PBINV"."INV403"."CKEY" = :ls_ckey  USING sqlca;

IF sqlca.sqlcode = 0 Then	
	IF ls_xyear = left(g_s_date,6) Then		//년월비교
		ln_ctlno = ln_ctlno + 1
	Else    
		ls_xyear = left(g_s_date,6)
		ln_ctlno = 1
	End IF
	//자재 Control DB Update
	UPDATE "PBINV"."INV403"
	SET    "SRNO" = :ln_ctlno,
			 "XYEAR" = :ls_xyear
	WHERE "PBINV"."INV403"."COMLTD" = '01' 
	  AND "PBINV"."INV403"."CKEY" = :ls_ckey  USING sqlca;
	
	IF sqlca.sqlcode <> 0 Then
		MessageBox("Inv403 upt SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
		Rollback using sqlca;			
		Return 'er'
	End IF
	
	ls_purno = g_s_deptcd + mid(g_s_date,4,3) + String(ln_ctlno,"0000")
	
ElseIF sqlca.sqlcode = 100 Then //Data Not Found
	ls_purno = g_s_deptcd + mid(g_s_date,4,3) + String(1,"0000")	//발주번호
	ls_xyear = left(g_s_date,6)
	ln_ctlno = 1
	
	//Control DB
	INSERT INTO "PBINV"."INV403"  
         ( "COMLTD",  "CKEY",   "SRNO",    "JSCD",   "FROMDT",   "TODT",   "XYEAR",     "XYEAR1",   
           "RPFL",    "ISFL",   "RMRT",    "EXDTC",  "VANCD",    "PGMID",  "XCOMMENT",  "EXTD",   
           "INPTID",      "INPTDT",      "UPDTID",     "UPDTDT",      "IPADDR",         "MACADDR" )  
  VALUES ( '01',    :ls_ckey ,  :ln_ctlno,   ' ',      ' ',        ' ',     :ls_xyear,    ' ',   
           ' ',     ' ',       ' ',        ' ',      ' ',        ' ',     ' ',          ' ',   
           :g_s_empno,   :g_s_datetime,  :g_s_empno,   :g_s_datetime,   :g_s_ipaddr,    :g_s_macaddr )  ;
			  
	IF sqlca.sqlcode <> 0 Then			  
		MessageBox("Inv403 ins SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
		Rollback Using sqlca;		
		Return 'er'
	End IF
Else
	MessageBox("SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
	Return "er"	
End IF

/////////////////////////// 발주 Head 생성 /////////////////////////////////////
IF ag_s_gubun = 'N' Then
	//발주 Head 생성 안함.
Else
	//업체 Master DB
	  SELECT "PBPUR"."PUR101"."TOD"  
		INTO :ls_tod  
	  FROM "PBPUR"."PUR101"  
	  WHERE ( "PBPUR"."PUR101"."COMLTD" = '01' ) AND  
			  ( "PBPUR"."PUR101"."VSRNO" = :ag_s_vsrno ) Using sqlca;
	
	IF sqlca.sqlcode <> 0 Then	
		ls_tod = ' ' 
	End IF
	
	//정상,취소 발주구분
	IF ag_s_gubun = "1" Then
		ls_pgubu = ' '  //정상
	Else
		ls_pgubu = 'C'  //취소
	End IF
	
	//발주Head 생성 
	INSERT INTO "PBPUR"."PUR401"  
		 ( "COMLTD",  "PURNO",   "XPLAN",   "VSRNO",   "XPLANT",   "DIV",   "XUSE",   "PINDT",   "TOD",     "PPRT",
			"PGUBU",   "EXTD",    "INPTID",   "INPTDT",   "UPDTID", "UPDTDT",   "IPADDR",   "MACADDR" )  
	VALUES ( '01', :ls_purno, :ag_s_xplan, :ag_s_vsrno, :ag_s_xplant, :ag_s_div, :ag_s_xuse, :g_s_date, :ls_tod,  '1',   
				:ls_pgubu, ' ',  :g_s_empno, :g_s_datetime, :g_s_empno, :g_s_datetime, :g_s_ipaddr,  :g_s_macaddr )  Using sqlca;
	
	IF sqlca.sqlcode <> 0 Then	
		MessageBox("Inv401 ins SQL error/" +string(sqlca.sqlcode), SQLCA.SQLErrText)
		Rollback Using sqlca;	
		Return "er"
	End IF
End IF

Return ls_purno
end function

