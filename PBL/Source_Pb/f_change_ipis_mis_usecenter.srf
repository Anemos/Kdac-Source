$PBExportHeader$f_change_ipis_mis_usecenter.srf
$PBExportComments$간판 불출처 변경
global type f_change_ipis_mis_usecenter from function_object
end type

forward prototypes
global function integer f_change_ipis_mis_usecenter (ref string ag_message, string ag_orderseq, string ag_deliveryno, string ag_usecenter, string ag_costgubun, string ag_empno, any ag_str_ipis[])
end prototypes

global function integer f_change_ipis_mis_usecenter (ref string ag_message, string ag_orderseq, string ag_deliveryno, string ag_usecenter, string ag_costgubun, string ag_empno, any ag_str_ipis[]);string ls_chgcd, ls_xplant, ls_div, ls_itno, ls_tdte4, ls_id
String ls_cls, ls_srce, ls_costdiv, ls_xunit, ls_xplan, ls_pdcd, ls_mlan
String ls_fsrno, ls_srno, ls_srno1
String ls_prevsrno, ls_vsrno, ls_vndr, ls_vndnm
String ls_prexuse,  ls_xuse, ls_slno, ls_dept, ls_jscd
dec{0} ld_amt
dec{1} ld_qty
dec{4} ld_convqty
integer	li_cnt, li_index, li_Retcnt


str_ipis_server lstr_ipis

//***AS400 접속여부 체크, 저녁8-아침8 인터페이스 저장
li_index = f_ipis_server_get_transaction(ag_str_ipis, 'X', 'Z')

If li_index = -1 then
	ag_message = "A/S400, INT 서버 연결에 실패하였습니다."
	return -1
End if

lstr_ipis = ag_str_ipis[li_index]

//*** 업무로직 적용
ls_chgcd  = 'U'
ls_fsrno  = ag_orderSeq
ls_xuse   = ag_costgubun
ls_dept   = ag_usecenter
ls_slno   = ag_deliveryNo
ls_id     = ag_empno

if isnull(ls_xuse) then ls_xuse = '01'
if isnull(ls_dept) then ls_dept = ''
if isnull(ls_slno) then ls_slno = ''

if ls_xuse = 'Y' then
	ls_xuse = '07'
elseif ls_xuse = 'N' then
	ls_xuse = '04'
else
	ls_xuse = ls_xuse
end if

//*** 전산번호 check **
ls_srno  = Mid(ls_fsrno, 1, 8)
ls_srno1 = Mid(ls_fsrno, 9, 2)

//*** Detail, check **
SELECT XPLANT, DIV, ITNO, XUSE, VSRNO, TDTE4, TQTY4, TRAMT
	into :ls_xplant, :ls_div, :ls_itno, :ls_prexuse, :ls_prevsrno, :ls_tdte4, :ld_qty, :ld_amt
FROM PBINV.INV401
WHERE COMLTD='01' AND SLIPTYPE = 'IS' AND SRNO= :ls_srno AND SRNO1=:ls_srno1 AND SRNO2='01'  Using lstr_ipis.t_sqlpis;

If lstr_ipis.t_sqlpis.sqlcode <> 0 then
	ag_message = "불출정보 미존재"
	return -1 
End if

//*** 변경일자 check
If ls_prexuse = '07' then	
	ls_Jscd = f_Vndr_Jscd( 'D', ls_prevsrno )
	
	If f_Get_Ctlchk( ls_tdte4, ls_Jscd ) = -1 Then
		ag_message = "변경전 업체 유상사급 마감이후 작업 불가"
		return -1
	End If  
else
	If f_Get_Ctlchk( ls_tdte4, " " ) = -1 Then
		ag_message = "마감이후 작업 불가"
		return -1
	End If
End if

//*** item Balance Check
SELECT CLS, SRCE, PDCD, COSTDIV, XPLAN, MLAN, XUNIT, CONVQTY                                        
  INTO :ls_cls, :ls_srce, :ls_pdcd, :ls_costdiv, :ls_xplan, :ls_mlan, :ls_xunit, :ld_convqty        
FROM PBINV.INV101                                                             
WHERE COMLTD = '01' AND XPLANT = :ls_xplant AND DIV = :ls_div AND ITNO = :ls_itno Using lstr_ipis.t_sqlpis;

If lstr_ipis.t_sqlpis.sqlcode <> 0 then                                                          
	ag_message = "품목상세정보 미존재"
	return -1                                                           
End if

//*** 라인 OR 반출 업체 check	
If ls_xuse = '04' or ls_xuse ='07' then
	
	ls_vsrno = ls_dept
	ls_dept  = ''
	ls_vndr  = f_vndr_pur(ls_vsrno,'S','D')
	ls_vndnm = f_vdnm_pur(ls_vsrno,'S','D')
		
	if f_spacechk(ls_vsrno) = -1 Or Mid(ls_vsrno,1,1) <> 'D' then
		ag_message = "반출시 업체코드를 넘겨줘야 함."
		return -1
	End if
	
	//*** 업체 Master 등록여부 Check
	SELECT Count(*)  
		INTO :li_Retcnt
	FROM PBPUR.PUR101                                                             
	WHERE COMLTD = '01' AND VSRNO = :ls_vsrno AND SCGUBUN = 'S' AND DIGUBUN ='D' Using lstr_ipis.t_sqlpis;
	
	If li_Retcnt <> 1 then                                                          
		ag_message = "반출업체 Master정보 미존재"
		return -1                                                           
	End if
	
	//*** 유상사급일 경우 마감일 check
	if ls_xuse = '07' then 
		ls_Jscd = f_Vndr_Jscd( 'D', ls_vsrno )
		
		If f_Get_Ctlchk( ls_tdte4, ls_vsrno ) = -1 Then
			ag_message = "유상사급 마감이후 작업 불가"
			return -1
		End If 
		
		SELECT Count("ITNO")	INTO :li_cnt
		  FROM "PBINV"."INV304"  
		WHERE "COMLTD" = '01'    AND "XPLANT" = :ls_xplant AND
				"DIV"    = :ls_div AND "ITNO"   = :ls_itno using lstr_ipis.t_sqlpis;
					
		If li_cnt < 1 Then
			ag_message = "유상사급단가가 미존재품"
			return -1
		End If		
	End if
elseif ls_xuse = '01' then
	ls_dept = '  '
	ls_vsrno = ' '
	ls_vndr  = ' '
	ls_vndnm = ' '
Else
	ls_dept = ls_dept 
	ls_vsrno = ' '
	ls_vndr  = ' '
	ls_vndnm = ' '
End if 

UPDATE PBINV.INV401
	SET DEPT   = :ls_dept,
		 XUSE   = :ls_xuse,
		 VSRNO  = :ls_vsrno,
		 VNDR   = :ls_vndr,
		 VNDNM  = :ls_vndnm,
		 UPDTID = :ls_id,
		 UPDTDT = :g_s_datetime
 WHERE COMLTD ='01' AND SLIPTYPE = 'IS' AND SRNO = :ls_srno AND SRNO1 = :ls_srno1 AND SRNO2 ='01'  
       Using lstr_ipis.t_sqlpis;
				  
if lstr_ipis.t_sqlpis.sqlnrows <> 1 then
  ag_message = "Trans 데이터(간판불출) 업데이트에 실패하였습니다."
  f_ipis_transaction_errorlog(ls_chgcd,'','f_change_ipis_mis_usecenter',&
  lstr_ipis.t_sqlpis.sqlerrtext,'Trans 데이터(간판불출) 업데이트에 실패', g_s_empno, g_s_ipaddr, g_s_macaddr)
  return -1
 end if	
		 
	// 재공생성로직
li_retcnt = f_wip001_crud_logic( 'D', '01', 'IS', ls_srno, ls_srno1, '01', ls_xplant, ls_div, &
				ls_itno, ls_cls, ls_srce, ld_convqty, ls_prexuse, ' ', ' ', ls_prevsrno, ld_qty, ld_qty, &
				ls_tdte4, ls_tdte4, ld_amt, lstr_ipis.t_sqlpis, ag_message )
if li_retcnt = -1 then return -1

li_retcnt = f_wip001_crud_logic( 'C', '01', 'IS', ls_srno, ls_srno1, '01', ls_xplant, ls_div, &
				ls_itno, ls_cls, ls_srce, ld_convqty, ls_xuse, ' ', ls_dept, ls_vsrno, ld_qty, ld_qty, &
				ls_tdte4, ls_tdte4, ld_amt, lstr_ipis.t_sqlpis, ag_message )
if li_retcnt = -1 then return -1
		 

return 0
end function

