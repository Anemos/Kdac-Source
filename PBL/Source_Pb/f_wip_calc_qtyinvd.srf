$PBExportHeader$f_wip_calc_qtyinvd.srf
$PBExportComments$일일재공 창고재공DB에 수량 Update
global type f_wip_calc_qtyinvd from function_object
end type

forward prototypes
global function any f_wip_calc_qtyinvd (string a_cmcd, string a_plant, string a_dvsn, string a_adjdate)
end prototypes

global function any f_wip_calc_qtyinvd (string a_cmcd, string a_plant, string a_dvsn, string a_adjdate);string ls_itno, ls_sltype, ls_xuse, ls_itcl, ls_srce, ls_pdcd, ls_rtngub, ls_nuse
dec{4} lc_qty2, lc_qty3, lc_qty4, lc_usqty[1 to 8], lc_convqty, lc_inqty, lc_bgqty, lc_ohqty
dec{0} lc_tramt,lc_tsamt, lc_usamt[1 to 9], lc_inamt, lc_bgamt, lc_ohamt
dec{5} lc_avrgcost
integer li_cnt, li_rowcnt
long ll_totcnt, ll_totsum, ll_totcur
string ls_year, ls_month, ls_fromdt, ls_todt
string ls_yyyy, ls_mm, ls_nextdate
datastore ds_calc_wip003 

ls_year 		= mid(a_adjdate,1,4)
ls_month 	= mid(a_adjdate,5,2)
ls_fromdt 	= mid(a_adjdate,1,6) + '01'

// 로직 추가 끝
ds_calc_wip003 = create datastore                  		         
ds_calc_wip003.dataobject = "d_wip_calc_wip003d"
ds_calc_wip003.settransobject(sqlca)

li_rowcnt = ds_calc_wip003.retrieve(a_cmcd,a_plant,a_dvsn,a_adjdate)
if li_rowcnt < 1 then
	return true
end if

ll_totcnt = 0
ll_totsum = li_rowcnt
for li_cnt = 1 to li_rowcnt
	ls_itno = trim(ds_calc_wip003.getitemstring(li_cnt,"wip003d_wcitno"))
	lc_convqty = ds_calc_wip003.getitemdecimal(li_cnt,"inv101_convqty")
	lc_avrgcost = ds_calc_wip003.getitemdecimal(li_cnt,"wip003d_wcavrg1")
	if lc_convqty = 0 then
		lc_convqty = 1
	end if

	lc_usqty[] = { 0, 0, 0, 0, 0, 0, 0, 0}
	lc_inqty = 0
	lc_usamt[] = { 0, 0, 0, 0, 0, 0, 0, 0, 0}
	DECLARE invqty_cur CURSOR FOR  
		SELECT "PBINV"."INV401"."SLIPTYPE",   
				 "PBINV"."INV401"."XUSE",
				 "PBINV"."INV401"."RTNGUB",
				 sum("PBINV"."INV401"."TQTY2") ,
				 sum("PBINV"."INV401"."TQTY3") ,
				 sum("PBINV"."INV401"."TQTY4") ,
				 sum("PBINV"."INV401"."TRAMT"),
				 sum("PBINV"."INV401"."TSAMT") 
		FROM "PBINV"."INV401"  
		WHERE ( "PBINV"."INV401"."COMLTD" = :a_cmcd ) AND  
				( "PBINV"."INV401"."XPLANT" = :a_plant ) AND  
				( "PBINV"."INV401"."DIV" = :a_dvsn ) AND 
				( "PBINV"."INV401"."ITNO" = :ls_itno ) AND
				( "PBINV"."INV401"."TDTE4" >= :ls_fromdt )
		GROUP BY "PBINV"."INV401"."SLIPTYPE",   
				"PBINV"."INV401"."XUSE",
				"PBINV"."INV401"."RTNGUB" 
				using sqlca;
	OPEN invqty_cur;
	do while true
		FETCH invqty_cur INTO :ls_sltype,:ls_xuse,:ls_rtngub,:lc_qty2,:lc_qty3,:lc_qty4,:lc_tramt,:lc_tsamt;
			if sqlca.sqlcode <> 0 then
				exit
			end if
			
		choose case ls_sltype
			case 'IS','IP'
				choose case ls_xuse
					case '01','02'
						lc_usqty[1] = lc_usqty[1] + lc_qty4     				//라인불출수량
					case '04'
						lc_usqty[2] = lc_usqty[2] + lc_qty4     				//업체불출수량(무상)
					case '03','05','06'
						lc_usqty[5] = lc_usqty[5] + lc_qty4     				//타부서불출수량
					case '07'
						lc_usqty[3] = lc_usqty[3] + lc_qty4     				//업체불출수량(유상)
						lc_usamt[3] = lc_usamt[3] + lc_tramt
				end choose
			case 'RS'
				choose case ls_xuse
					case '01','02'		//라인
						if ls_rtngub <> '02' then
							lc_usqty[1] = lc_usqty[1] - lc_qty4 
						end if
					case '03','05','06'		//타부서
						if ls_rtngub <> '02' then
							lc_usqty[5] = lc_usqty[5] - lc_qty4 
						end if
					case '04'			//업체무상
						if ls_rtngub <> '02' then
							lc_usqty[2] = lc_usqty[2] - lc_qty4  
						end if
					case '07'			//업체업체유상
						lc_usqty[3] = lc_usqty[3] - lc_qty4 
						lc_usamt[3] = lc_usamt[3] - lc_tramt
				end choose
			case 'RP'
				lc_inqty = lc_inqty + lc_qty4
				if ls_itcl = '50' and ls_srce = '04' then
					if (a_plant = 'D' and (a_dvsn = 'A' or a_dvsn = 'V')) then
						//pass
					else
						SELECT NUSE INTO :ls_nuse
						FROM PBINV.INV101
						WHERE Comltd = '01' AND XPLANT = :a_plant AND DIV = :a_dvsn AND ITNO = :ls_itno
						using sqlca;
						
						if a_plant = 'B' or a_plant = 'K' then
							if ls_nuse <> 'Y' then
								lc_usqty[1] = lc_usqty[1] + lc_qty4
							end if
						else
							lc_usqty[1] = lc_usqty[1] + lc_qty4
						end if
					end if
				end if
			case 'SA','SR'
				if ls_sltype = 'SA' then
					if ls_itcl = '10' and ls_srce = '04' then
						lc_usqty[1] = lc_usqty[1] + lc_qty4     				//라인불출수량
					else
						lc_usqty[4] = lc_usqty[4] + lc_qty4                   //출하
//						lc_usamt[4] = lc_usamt[4] + lc_tramt
					end if
				else
					if ls_itcl = '10' and ls_srce = '04' then
						lc_usqty[1] = lc_usqty[1] - lc_qty4     				//라인불출수량
					else
						lc_usqty[4] = lc_usqty[4] - lc_qty4                   //출하취소
//						lc_usamt[4] = lc_usamt[4] - lc_tramt
					end if
				end if
			case 'IW'
				lc_usqty[6] = lc_usqty[6] + lc_qty4                      //이체불출
				if ls_itcl = '10' then
					lc_usamt[6] = lc_usamt[6] + (lc_tsamt - lc_tramt)
				else
					lc_usamt[6]    = lc_usamt[6] + lc_tsamt
				end if
			case 'RW'
				lc_usqty[6] = lc_usqty[6] - lc_qty4                      //이체입고
				if ls_itcl = '10' then
					lc_usamt[6] = lc_usamt[6] - (lc_tsamt - lc_tramt)
				else
					lc_usamt[6]    = lc_usamt[6] - lc_tsamt
				end if
			case 'SS'
				lc_usqty[8] = lc_usqty[8] + lc_qty4                      //입고취소(폐기처리)
		end choose
	loop
	CLOSE invqty_cur;
	
	//기초수량, 투입수량, 사용수량 데이타 입력
	lc_bgqty = ds_calc_wip003.getitemdecimal(li_cnt,"wip003d_wcbgqt")
	lc_bgamt = lc_bgqty * lc_avrgcost
	lc_ohqty = ds_calc_wip003.getitemdecimal(li_cnt,"wip003d_wcohqt")
	lc_ohamt = lc_ohqty * lc_avrgcost
	
	lc_inqty = lc_inqty * lc_convqty
	lc_inamt = lc_inqty * lc_avrgcost
	lc_usqty[1] = lc_usqty[1] * lc_convqty
	lc_usamt[1] = lc_usqty[1] * lc_avrgcost
	lc_usqty[2] = lc_usqty[2] * lc_convqty
	lc_usamt[2] = lc_usqty[2] * lc_avrgcost
	lc_usqty[3] = lc_usqty[3] * lc_convqty
	lc_usamt[3] = lc_usqty[3] * lc_avrgcost
	lc_usqty[4] = lc_usqty[4] * lc_convqty
	lc_usamt[4] = lc_usqty[4] * lc_avrgcost
	lc_usqty[5] = lc_usqty[5] * lc_convqty
	lc_usamt[5] = lc_usqty[5] * lc_avrgcost
	lc_usqty[6] = lc_usqty[6] * lc_convqty
	lc_usamt[6] = lc_usqty[6] * lc_avrgcost
	lc_usqty[8] = lc_usqty[8] * lc_convqty
	lc_usamt[8] = lc_usqty[8] * lc_avrgcost
	lc_usamt[9] = lc_bgamt + lc_inamt - (lc_usamt[1] + lc_usamt[2] + lc_usamt[3] + lc_usamt[4] + lc_usamt[5] + lc_usamt[6] + lc_usamt[8] + lc_ohamt)
	
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcbgqt",lc_bgqty)	
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcbgat1",lc_bgamt)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcbgat2",0)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcinqt",lc_inqty)	
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcinat1",lc_inamt)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcinat2",0)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt1",lc_usqty[1])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat1",lc_usamt[1])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt2",lc_usqty[2])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat2",lc_usamt[2])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt3",lc_usqty[3])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat3",lc_usamt[3])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt4",lc_usqty[4])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat4",lc_usamt[4])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt5",lc_usqty[5])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat5",lc_usamt[5])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt6",lc_usqty[6])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat6",lc_usamt[6])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat7",0)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusqt8",lc_usqty[8])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat8",lc_usamt[8])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcusat9",lc_usamt[9])
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcohqt",lc_ohqty)	
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcohat1",lc_ohamt)
	ds_calc_wip003.setitem(li_cnt,"wip003d_wcohat2",0)
	//프로그래스바에 진행현황을 나타냄
	ll_totcnt = ll_totcnt + 1
next

if ds_calc_wip003.update() = 1 then
	destroy ds_calc_wip003
	return true
else
	messagebox("에러",string(sqlca.sqlcode) + sqlca.sqlerrtext)
	destroy ds_calc_wip003
	return false
end if
end function

