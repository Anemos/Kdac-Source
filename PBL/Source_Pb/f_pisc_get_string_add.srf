$PBExportHeader$f_pisc_get_string_add.srf
$PBExportComments$입력문자 1증가 함수 f_pisr_string_1add( string ) return string + 1
global type f_pisc_get_string_add from function_object
end type

forward prototypes
global function boolean f_pisc_get_string_add (string as_orgstring, ref string rs_addstring)
end prototypes

global function boolean f_pisc_get_string_add (string as_orgstring, ref string rs_addstring);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		: f_pisc_get_addstring
//	Access		: reorder
//	Arguments	: string		as_orgstring	문자값
//					  string		rs_addstring	증가된 값 => 입력문자값 + 1 ( 36진수 증가치 )
//	Returns		: Boolean	성공 여부
//	Description	: 업체별 품번을 Unique하게 구분하기위해 Key값 Count ( 0~9,A~Z 의 36진수 문자값 증가 )
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Gwon Oh_Sang
// Coded Date	: 2002.09.04
///////////////////////////////////////////////////////////////////////////////////////////////////////////
// 만약 2자리 일 경우
// 00 ~ 99 까지 증가
// 99 다음에 A0..A1..A2..AA...ZZ 까지 증가

integer li_length, i, li_ascii
string ls_string[], ls_result, ls_max, ls_max_mid

ls_max = ''
ls_max_mid = ''
li_length = Len(as_orgstring)

For i = 1 To li_length
	ls_max = ls_max + 'Z'
Next

If as_orgstring >= ls_max Then 
	Return False
End If 

For i = 1 To li_length
	ls_max_mid = ls_max_mid + '9'
Next

If ls_max_mid > as_orgstring Then
	ls_result = Right('0000000000' + String(Integer(as_orgstring) + 1), li_length)
Else
	For i = 1 To li_length
		ls_string[i] = Mid(as_orgstring, i , 1)
	Next
	
	Do Until i < 1
		i = i - 1
		li_ascii = ASC(ls_string[i]) + 1 
		
		If li_ascii >= 48 And li_ascii <= 57 Then 
			ls_string[i] = char(li_ascii)
			Exit
		ElseIf li_ascii > 57 And li_ascii < 65 Then
			ls_string[i] = 'A'
			exit
		ElseIf li_ascii >= 65 And li_ascii <= 90 Then
			ls_string[i] = char(li_ascii)
			Exit
		ElseIf li_ascii > 90 Then 
			ls_string[i] = '0'
		End If
	Loop
	
	ls_result = ''
	
	For i = 1 To li_length
		ls_result = ls_result + ls_string[i]
	Next
End If

rs_addstring	= ls_result

Return True
end function

