$PBExportHeader$f_inv_customer_acc.srf
$PBExportComments$고객사유상사급원재료공제분(회계중간파일)
global type f_inv_customer_acc from function_object
end type

forward prototypes
global function integer f_inv_customer_acc (string as_xyear, string as_month)
end prototypes

global function integer f_inv_customer_acc (string as_xyear, string as_month);///////////////////////////////////////////////////////////////////
// 설명   : 고객사유상사급재고DB(PCC951) 에 원재료 고객사유상 공제분을 
//          해당년월 데이타를 입력한다.   
//
// Arg    : string 작업년, string 작업월
// Return : Int number 에러코드 
//
//////////////////////////////////////////////////////////////////
String ls_xplant, ls_div, ls_xyear, ls_month, ls_costdiv, ls_pdcd
Dec{0} ld_amt
Int    li_ret


DELETE FROM PBACC.PCC951
WHERE COMLTD = '01' AND SLYEAR = :as_xyear AND SLMONTH = :as_month AND SLGUBUN = '01' Using sqlca;

IF SQLCA.SQLCODE <> 0 THEN
	RETURN 1
END IF

DECLARE 	bom_cur CURSOR FOR

SELECT   I."XPLANT",
			I."DIV",
			I."CXYEAR",
			I."CMONTH",
			I."COSTDIV", 						
			I."PDCD",
			COALESCE(I."BAMT", 0) + I."IAMT" - I."OAMT"
	FROM ( SELECT  B."XPLANT",
						B."DIV",
						B."CXYEAR",
                  B."CMONTH",
                  B."COSTDIV", 						
						B."PDCD",
                  SUM( B."BGAMT"  ) AS BAMT,  
						SUM( B."INTAMT" ) AS IAMT,
						SUM( B."OUTAMT" ) AS OAMT
				 FROM "PBCOMMON"."DAC007" A,
						( SELECT X."COMLTD",
									X."XPLANT",
									X."DIV",
                           X."ITNO",
									SUBSTR(X."XYEAR", 1, 4) AS CXYEAR,
                           SUBSTR(X."XYEAR", 5, 2) AS CMONTH,
                           X."COSTDIV",
									X."PDCD",
                           TRUNCATE((X."BGUQTY"+X."BGRQTY"+X."BGSQTY") * 
                                    CASE WHEN Y."FCOCST" > 0 THEN Y."FCOCST"
                                         WHEN Y."FCMCST" > 0 THEN Y."FCMCST"
                                    ELSE Y."FCICST" END, 0) AS BGAMT, 
                           TRUNCATE(X."INTQTY" * 
                                    CASE WHEN Y."FCOCST" > 0 THEN Y."FCOCST"
                                         WHEN Y."FCMCST" > 0 THEN Y."FCMCST"
                                    ELSE Y."FCICST" END, 0) AS INTAMT,
									TRUNCATE(X."OUTQTY" * 
                                    CASE WHEN Y."FCOCST" > 0 THEN Y."FCOCST"
                                         WHEN Y."FCMCST" > 0 THEN Y."FCMCST"
                                    ELSE Y."FCICST" END, 0) AS OUTAMT
							FROM  "PBINV"."INV402" X, "PBPDM"."BOM016" Y
						  WHERE  X."COMLTD" = Y."FCMCD"  AND
                           X."XPLANT" = Y."FPLANT" AND
                           X."DIV"    = Y."FDVSN"  AND
                           X."ITNO"   = Y."FMDNO"  AND
                           X."XYEAR"  = Y."FDATE"  AND
                           Y."FGUBUN" = 'B'        AND
                           X."COMLTD" = '01'       AND
									X."XYEAR"  = :as_xyear || :as_month  AND
									X."CLS"    = '10'          AND
									X."SRCE" IN ( '02', '04' ) AND
									( X."BGAMT" <> 0 OR X."INTAMT" <> 0 OR X."OUTAMT" <> 0 OR X."OHAMT" <> 0 )		
						) B
			WHERE A."COMLTD" = B."COMLTD" AND
					A."PRPRCD" = B."PDCD"		
			GROUP BY B."XPLANT",
						B."DIV",
						B."CXYEAR",
                  B."CMONTH",
                  B."COSTDIV", 						
						B."PDCD"
			)	I
ORDER BY 1, 2, 3, 4, 5, 6 Using sqlca;

if sqlca.sqlcode <> 0 then 
	Return 2
End if
		
OPEN bom_cur;  // 조건에 맞는 data open

FETCH bom_cur INTO :ls_xplant, :ls_div, :ls_xyear, :ls_month, :ls_costdiv, :ls_pdcd, :ld_amt ; 

DO WHILE Sqlca.SqlCode = 0
	
	IF ld_amt > 0 then 

		//공제분 입력
		insert into "PBACC"."PCC951"    
				 ( "COMLTD",      "SLYEAR",      "SLMONTH",     "SLGUBUN",     "SLDIV",
					"SLPRCD",      "SLAMTW",      "XPLANT",      "DIV",         "EXTD",
					"INPTID",      "INPTDT",		"UPDTID",      "UPDTDT",      "IPADDR",      "MACADDR" )  
		VALUES ( :g_s_company,   :ls_xyear,    :ls_month,      '01',         :ls_xplant||:ls_costdiv,
					:ls_pdcd,       :ld_amt,      :ls_xplant,     :ls_div,      ' ',
					:g_s_empno,     :g_s_date,	   :g_s_empno,     :g_s_datetime,:g_s_ipaddr,   :g_s_macaddr  )  using sqlca;
	
		IF Sqlca.SqlCode <> 0 THEN	
			RETURN 3		 
		end if
	End if

	FETCH bom_cur INTO :ls_xplant, :ls_div, :ls_xyear, :ls_month, :ls_costdiv, :ls_pdcd, :ld_amt ;
		
LOOP

CLOSE bom_cur;

return 0
end function

