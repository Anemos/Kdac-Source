$PBExportHeader$f_pisc_get_kbno_next_normal.srf
$PBExportComments$정규간판의 다음 번호를 구한다.
global type f_pisc_get_kbno_next_normal from function_object
end type

forward prototypes
global function boolean f_pisc_get_kbno_next_normal (string as_orgstring, ref string rs_addstring)
end prototypes

global function boolean f_pisc_get_kbno_next_normal (string as_orgstring, ref string rs_addstring);///////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Function		: f_pisc_get_addstring
//	Access		: reorder
//	Arguments	: string		as_orgstring	문자값
//					  string		rs_addstring	증가된 값 => 입력문자값 + 1 ( 36진수 증가치 )
//	Returns		: Boolean	성공 여부
//	Description	: 업체별 품번을 Unique하게 구분하기위해 Key값 Count ( 0~9,A~Z 의 36진수 문자값 증가 )
// Company		: DAEWOO Information System Co., Ltd. IAS
// Author		: Gwon Oh_Sang
// Coded Date	: 2002.09.04
///////////////////////////////////////////////////////////////////////////////////////////////////////////
integer li_length, i, li_ascii
string ls_string[], ls_result, ls_max, ls_max_mid

ls_max = ''
ls_max_mid = ''
li_length = Len(as_orgstring)

For i = 1 To li_length
	If i = 1 Then
		ls_max = ls_max + 'W'	// W 까지만 사용한다...임시 간판이 X로 시작하기 땜시..
	Else
		ls_max = ls_max + 'Z'
	End If
Next

If as_orgstring >= ls_max Then 
	Return False
End If 

For i = 1 To li_length
	ls_max_mid = ls_max_mid + '9'
Next

If ls_max_mid > as_orgstring Then
	ls_result = Right('0000000000' + String(Integer(as_orgstring) + 1), li_length)
Else
	For i = 1 To li_length
		ls_string[i] = Mid(as_orgstring, i , 1)
	Next
	
	Do Until i < 1
		i = i - 1
		li_ascii = ASC(ls_string[i]) + 1 
		
		If li_ascii >= 48 And li_ascii <= 57 Then 
			ls_string[i] = char(li_ascii)
			Exit
		ElseIf li_ascii > 57 And li_ascii < 65 Then
			ls_string[i] = 'A'
			exit
		ElseIf li_ascii >= 65 And li_ascii <= 90 Then
			ls_string[i] = char(li_ascii)
			Exit
		ElseIf li_ascii > 90 Then 
			ls_string[i] = '0'
		End If
	Loop
	
	ls_result = ''
	
	For i = 1 To li_length
		ls_result = ls_result + ls_string[i]
	Next
End If

rs_addstring	= ls_result

Return True
end function

