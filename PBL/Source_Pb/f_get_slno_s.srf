$PBExportHeader$f_get_slno_s.srf
$PBExportComments$불출 반납전표번호 Return
global type f_get_slno_s from function_object
end type

forward prototypes
global function string f_get_slno_s (string as_deptcd)
end prototypes

global function string f_get_slno_s (string as_deptcd);//////////////////////////////////////////////
// * 불출 반납시 불출, 반납전표번호 Return *
//
//
// Return: 불출전표번호(성공), ""(실패)
//////////////////////////////////////////////

String	ls_Ckey

ls_Ckey = as_DeptCd + '00'


Int	li_Srno

SELECT SRNO INTO :li_Srno
	FROM PBINV.INV403
WHERE COMLTD = :g_s_company AND CKEY = :ls_Ckey	Using	SQLCA ;


If SQLCA.SQLCode = -1 Then      // SQL Error.
	Return ""
	
ElseIf SQLCA.SQLCode = 100 Then // 해당 Row가 없음.
	li_Srno = 1
	
	INSERT INTO "PBINV"."INV403"  
	 ( "COMLTD",   "CKEY",   "SRNO",    "JSCD",   "FROMDT",   "TODT",   "XYEAR",      "XYEAR1",   
		"RPFL",     "ISFL",   "RMRT",    "EXDTC",  "VANCD",    "PGMID",  "XCOMMENT",   "EXTD",   
		"INPTID",   "INPTDT", "UPDTID",  "UPDTDT", "IPADDR",   "MACADDR" )  
	VALUES
	 ( :g_s_company, :ls_Ckey,    :li_Srno, ' ',      ' ',        ' ',      ' ',          ' ',   
		' ',               ' ',      ' ',       ' ',      ' ',        ' ',      ' ',           ' ',   
		:g_s_empno, :g_s_datetime, :g_s_empno, :g_s_datetime, :g_s_ipaddr, :g_s_macaddr )  Using SQLCA ;
	
	If SQLCA.SQLCode <> 0 Then Return ""  // SQL(INSERT) Error.
	
ElseIf SQLCA.SQLCode = 0 Then // Success.
	If li_Srno = 9999 Then 
		li_Srno = 1
	Else
		li_Srno ++
	End If
	
	UPDATE PBINV.INV403
		SET SRNO    = :li_Srno,
	
			 UPDTID  = :g_s_empno,
			 UPDTDT  = :g_s_datetime,
			 IPADDR  = :g_s_ipaddr,
			 MACADDR = :g_s_macaddr
	WHERE	COMLTD = :g_s_company AND CKEY = :ls_Ckey	Using	SQLCA ;
	
	If SQLCA.SQLCode <> 0 Then Return ""  // SQL(UPDATE) Error.
End If

Return	as_DeptCd + Mid( g_s_date, 4, 3 ) + String( li_Srno, '0000' )
end function

