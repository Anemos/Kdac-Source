$PBExportHeader$f_wip_cost_ohat.srf
$PBExportComments$사용/재공금액 계산 Sub Function
global type f_wip_cost_ohat from function_object
end type

forward prototypes
global function integer f_wip_cost_ohat (ref datastore a_ds, string a_adjdate, string a_postdate, long a_curcnt, string a_gubun)
end prototypes

global function integer f_wip_cost_ohat (ref datastore a_ds, string a_adjdate, string a_postdate, long a_curcnt, string a_gubun);//**************************************************************************
// 재공금액 및 사용금액 계산 - 재공이월 DB(WIP002)
// a_gubun : 창고재공(INV), 라인업체재공(WIP)
//**************************************************************************
integer li_sqlcode, li_count
long ll_cnt, ll_currow, ll_chkcnt
string ls_cmcd, ls_plant, ls_dvsn, ls_orct, ls_itno, ls_srce, ls_itcl, ls_sltype, ls_xuse, ls_year, ls_month
string ls_iocd
dec{5} lc_avgcost
dec{4} lc_sumusqt ,lc_diffqty, lc_bgqty, lc_inqty, lc_ohqty, lc_convqty
dec{4} lc_usqty[1 to 10], lc_qty2, lc_qty3, lc_qty4
dec{0} lc_sumusat, lc_diffamt, lc_ohamt, lc_bgamt, lc_inamt, lc_inamt2, lc_remamt
dec{0} lc_usamt[1 to 10]

lc_usqty[] = { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0}   //정상,반출,유상반출,출하,타계정,타부서,불량,조정,       0,실사
lc_usamt[] = { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0}   //정상,반출,유상반출,출하,타계정,타부서,불량,조정,금액조정,실사
if a_gubun = 'wip' then
	ls_cmcd = a_ds.getitemstring(a_curcnt,"wip002_wbcmcd")
	ls_plant = a_ds.getitemstring(a_curcnt,"wip002_wbplant")
	ls_dvsn = a_ds.getitemstring(a_curcnt,"wip002_wbdvsn")
	ls_iocd = a_ds.getitemstring(a_curcnt,"wip002_wbiocd")
	ls_orct = trim(a_ds.getitemstring(a_curcnt,"wip002_wborct"))
	ls_itno = trim(a_ds.getitemstring(a_curcnt,"wip002_wbitno"))
	ls_srce = a_ds.getitemstring(a_curcnt,"inv402_srce")
	ls_itcl = a_ds.getitemstring(a_curcnt,"inv402_cls")
	lc_bgamt = a_ds.getitemdecimal(a_curcnt,"wip002_wbbgat1")
	lc_bgqty = a_ds.getitemdecimal(a_curcnt,"wip002_wbbgqt")
	lc_inamt = a_ds.getitemdecimal(a_curcnt,"wip002_wbinat1") + a_ds.getitemdecimal(a_curcnt,"wip002_wbinat3")
	lc_inamt2 = a_ds.getitemdecimal(a_curcnt,"wip002_wbinat2")
	lc_inqty = a_ds.getitemdecimal(a_curcnt,"wip002_wbinqt")
	lc_avgcost = a_ds.getitemdecimal(a_curcnt,"wip002_wbavrg1")
//****************************************************************************
// 1. 재공금액 계산 = 기말재공량 * 단가
// 2. 사용금액 = ( 기초 + 투입) - 재공금액
//****************************************************************************
	lc_usqty[1] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt1")
	lc_usqty[2] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt2")
	lc_usqty[3] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt3")
	lc_usqty[4] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt4")
	lc_usqty[5] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt5")
	lc_usqty[6] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt6")
	lc_usqty[7] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt7")
	lc_usqty[8] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqt8")
	//업체대금정산
	lc_usamt[9] = lc_inamt2
	//전월선불출금액
	if lc_bgqty <> 0 and lc_bgamt = 0 then
		lc_usamt[9] = lc_usamt[9] - (lc_bgqty * lc_avgcost)
	end if
	//2006.12.31 금액조정 502352981 => 0
	if ls_plant = 'D' and ls_dvsn = 'A' and ls_itno = '211428' and ls_orct = '9999' and a_adjdate = '200612' then
		lc_usamt[9] = 0
	end if
	//당월선불출금액
	if lc_inqty <> 0 and lc_inamt = 0 then
		lc_usamt[9] = lc_usamt[9] - (lc_inqty * lc_avgcost)
	end if
	lc_usqty[10] = a_ds.getitemdecimal(a_curcnt, "wip002_wbusqta")
	lc_sumusqt   = lc_usqty[1] + lc_usqty[2] + lc_usqty[3] + lc_usqty[4] + lc_usqty[5] + lc_usqty[6] &
							+ lc_usqty[7] + lc_usqty[8] + lc_usqty[10]
	lc_ohqty     = lc_bgqty + lc_inqty - lc_sumusqt
	lc_ohamt     = lc_ohqty * lc_avgcost
	//2006.12.31 현재공금액 0
	if ls_plant = 'D' and ls_dvsn = 'A' and ls_itno = '211428' and ls_orct = '9999' and a_adjdate = '200612' then
		lc_ohamt = 0
	end if
	lc_sumusat   = lc_bgamt + lc_inamt + lc_inamt2 - lc_ohamt
	
	lc_usamt[1]  = lc_usqty[1] * lc_avgcost
	lc_usamt[2]  = lc_usqty[2] * lc_avgcost
	lc_usamt[3]  = lc_usqty[3] * lc_avgcost
	lc_usamt[4]  = lc_usqty[4] * lc_avgcost
	lc_usamt[5]  = lc_usqty[5] * lc_avgcost
	lc_usamt[6]  = lc_usqty[6] * lc_avgcost
	lc_usamt[7]  = lc_usqty[7] * lc_avgcost
	lc_usamt[8]  = lc_usqty[8] * lc_avgcost
	lc_usamt[10] = lc_usqty[10] * lc_avgcost

	lc_diffamt   = lc_sumusat - ( lc_usamt[1] + lc_usamt[2] + lc_usamt[3] + lc_usamt[4] + lc_usamt[5] &
											+ lc_usamt[6] + lc_usamt[7] + lc_usamt[8] + lc_usamt[9] + lc_usamt[10])
	if lc_diffamt <> 0 then
		//차이금액 배분(2003.02.13 수정 : 현재공수량이 0인경우 차이금액을 금액조정으로 반영)
		if ( lc_sumusqt = 0 ) or (lc_usamt[1] = 0 and lc_usamt[2] = 0 ) then
			lc_usamt[9] = lc_usamt[9] + lc_diffamt
		else
			ll_cnt = 1
			ll_chkcnt = 0
			lc_remamt = lc_diffamt
			if ls_orct = '9999' then
				if lc_usamt[1] <> 0 then
					lc_usamt[1] = lc_usamt[1] + lc_diffamt
//				elseif lc_ohamt <> 0 then
//					lc_ohamt = lc_ohamt + lc_diffamt
				else
					lc_usamt[9] = lc_usamt[9] + lc_diffamt
				end if
			else
				lc_usamt[2] = lc_usamt[2] + lc_diffamt
			end if
				
//			do while( ll_cnt <= 8 )
//				if lc_usqty[ll_cnt] > 0  then
//					ll_chkcnt = ll_cnt
//					lc_usamt[ll_cnt]  = lc_usamt[ll_cnt] + ((lc_usqty[ll_cnt] / lc_sumusqt) * lc_diffamt)
//					lc_remamt = lc_remamt - ((lc_usqty[ll_cnt] / lc_sumusqt) * lc_diffamt)
//				end if
//				ll_cnt = ll_cnt + 1
//			loop
//			if lc_remamt <> 0 then
//				if ll_chkcnt = 0 then
//					lc_ohamt = lc_ohamt + lc_remamt
//				else
//					lc_usamt[ll_chkcnt] = lc_usamt[ll_chkcnt] + lc_remamt
//				end if
//			end if
		end if
	end if
	
	for ll_cnt = 1 to 9										//내역별 사용금액 반영
			a_ds.setitem(a_curcnt, "wip002_wbusat" + string(ll_cnt),lc_usamt[ll_cnt])
	next
	a_ds.setitem(a_curcnt, "wip002_wbusata",lc_usamt[10])
	
	ls_year = mid(a_postdate,1,4)
	ls_month = mid(a_postdate,5,2)
	//재공금액 UPDATE
	UPDATE "PBWIP"."WIP002"  
		SET "WBAVRG1" = :lc_avgcost, "WBBGQT" = :lc_ohqty, "WBBGAT1" = :lc_ohamt  
		WHERE ( "PBWIP"."WIP002"."WBCMCD" = :ls_cmcd ) AND  
				( "PBWIP"."WIP002"."WBPLANT" = :ls_plant ) AND  
				( "PBWIP"."WIP002"."WBDVSN" = :ls_dvsn ) AND  
				( "PBWIP"."WIP002"."WBORCT" = :ls_orct ) AND  
				( "PBWIP"."WIP002"."WBITNO" = :ls_itno ) AND 
				( "PBWIP"."WIP002"."WBIOCD" = :ls_iocd ) AND
				( "PBWIP"."WIP002"."WBYEAR"||"PBWIP"."WIP002"."WBMONTH" = :a_postdate )  using sqlca;
	if sqlca.sqlcode = 0 and sqlca.sqlnrows > 0 then
		//pass
	else
		insert into pbwip.wip002(wbcmcd,wbplant,wbdvsn,wborct,wbitno,
       	wbyear,wbmonth,wbrev,wbiocd,wbitcl,wbsrce,wbpdcd,
       	wbunit,wbtype,wbdesc,wbspec,wbscrp,wbretn,wbavrg1,
       	wbavrg2,wbbgqt,wbbgat1,wbbgat2,wbinqt,wbinat1,
       	wbinat2,wbinat3,wbinat4,wbusqt1,wbusat1,wbusqt2,
       	wbusat2,wbusqt3,wbusat3,wbusqt4,wbusat4,wbusqt5,
       	wbusat5,wbusqt6,wbusat6,wbusqt7,wbusat7,wbusqt8,
       	wbusat8,wbusat9,wbusqta,wbusata,wbplan,wbipaddr,
       	wbmacaddr,wbinptdt,wbupdtdt)
		select wbcmcd,wbplant,wbdvsn,wborct,wbitno,
       	:ls_year,:ls_month,wbrev,wbiocd,wbitcl,wbsrce,wbpdcd,
       	wbunit,wbtype,wbdesc,wbspec,wbscrp,wbretn,:lc_avgcost,
       	0,:lc_ohqty, :lc_ohamt,
       	0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
       	0,0,0,0,0,' ',' ',' ',' ',' '
			from pbwip.wip002
			where wbcmcd = :ls_cmcd and wbplant = :ls_plant and
					wbdvsn = :ls_dvsn and wborct = :ls_orct and
					wbitno = :ls_itno and wbyear||wbmonth = :a_adjdate and
					wbiocd = :ls_iocd
			using sqlca;
		if sqlca.sqlcode <> 0 then
			f_errorlog_insert(sqlca.sqlcode, sqlca.sqlerrtext)
		end if
	end if
	//재공밸런스 기초 UPDATE
	UPDATE "PBWIP"."WIP001"  
		SET "WAAVRG1" = :lc_avgcost,"WABGQT" = :lc_ohqty, "WABGAT1" = :lc_ohamt  
		WHERE ( "PBWIP"."WIP001"."WACMCD" = :ls_cmcd ) AND  
				( "PBWIP"."WIP001"."WAPLANT" = :ls_plant ) AND  
				( "PBWIP"."WIP001"."WADVSN" = :ls_dvsn ) AND  
				( "PBWIP"."WIP001"."WAORCT" = :ls_orct ) AND  
				( "PBWIP"."WIP001"."WAITNO" = :ls_itno ) AND  
				( "PBWIP"."WIP001"."WAIOCD" = :ls_iocd )
				using sqlca;
	if sqlca.sqlcode = 0 and sqlca.sqlnrows > 0 then
		return 0
	else
		//Data Creation
		insert into pbwip.wip001(wacmcd,waplant,wadvsn,waorct,
  			waitno,waiocd,waavrg1,waavrg2,wabgqt,wabgat1,wabgat2,
  			wainqt,wainat1,wainat2,wainat3,wainat4,wausqt1,wausat1,
  			wausqt2,wausat2,wausqt3,wausat3,wausqt4,wausat4,
  			wausqt5,wausat5,wausqt6,wausat6,wausqt7,wausat7,
  			wausqt8,wausat8,wausat9,waohqt,waohat1,waohat2,wascrp,
  			waretn,waplan,waipaddr,wamacaddr,wainptdt,waupdtdt)
  		values (:ls_cmcd,:ls_plant,:ls_dvsn,:ls_orct,:ls_itno,
  			:ls_iocd,:lc_avgcost,0,:lc_ohqty,:lc_ohamt,0,0,0,0,0,0,0,0,0,0,0,0,
  			0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,' ',
  			' ',' ',' ',' ')
		using sqlca;
		if sqlca.sqlcode = 0 then
			return 0
		else
			f_errorlog_insert(sqlca.sqlcode, sqlca.sqlerrtext)
			return -1
		end if
	end if
else          
	//재공창고 DB의 재공금액및 사용금액 계산
	ls_cmcd    = a_ds.getitemstring(a_curcnt,"wip003_wccmcd")
	ls_plant   = a_ds.getitemstring(a_curcnt,"wip003_wcplant")
	ls_dvsn    = a_ds.getitemstring(a_curcnt,"wip003_wcdvsn")
	ls_itno    = trim(a_ds.getitemstring(a_curcnt,"wip003_wcitno"))
	ls_srce    = a_ds.getitemstring(a_curcnt,"inv402_srce")
	ls_itcl    = a_ds.getitemstring(a_curcnt,"inv402_cls")
	lc_bgamt   = a_ds.getitemdecimal(a_curcnt,"wip003_wcbgat1")
	lc_inamt   = a_ds.getitemdecimal(a_curcnt,"wip003_wcinat1")
	lc_avgcost = a_ds.getitemdecimal(a_curcnt,"wip003_wcavrg1")
	lc_ohqty   = a_ds.getitemdecimal(a_curcnt,"com_ohqty")					//자재기말수량
	//lc_convqty = a_ds.getitemdecimal(a_curcnt,"inv101_convqty")
	
	//사용금액계산 : 자재TRANS에서 라인불출수량, 업체반출수량, 출하수량, 유상반출수량, 타부서불출수량, 이체수량
	//라인불출수량과 업체반출수량이 0보다 클경우 금액은 재공투입금액으로 처리됨 
	lc_usqty[1] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt1")     				//라인불출수량
	lc_usqty[2] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt2")    				//업체불출수량(무상)
	lc_usqty[3] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt3")               //출하
	lc_usqty[4] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt4")     				//업체불출수량(유상)
	lc_usqty[5] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt5")     				//타부서불출수량
	lc_usqty[6] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt6")               //이체불출
	lc_usamt[6] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusat6")					//자재이체불출금액을 가지고 있음
	lc_usqty[8] = a_ds.getitemdecimal(a_curcnt,"wip003_wcusqt8")               //입고취소(폐기처리)
	
	lc_sumusqt   = lc_usqty[1] + lc_usqty[2] + lc_usqty[3] + lc_usqty[4] + lc_usqty[5] + lc_usqty[6] + lc_usqty[8]
	lc_ohamt     = lc_ohqty * lc_avgcost
	lc_sumusat   = lc_bgamt + lc_inamt - lc_ohamt
	
	lc_usamt[1]  = lc_usqty[1] * lc_avgcost
	lc_usamt[2]  = lc_usqty[2] * lc_avgcost
	lc_usamt[3]  = lc_usqty[3] * lc_avgcost
	lc_usamt[4]  = lc_usqty[4] * lc_avgcost
	lc_usamt[5]  = lc_usqty[5] * lc_avgcost
	lc_usamt[6]  = lc_usqty[6] * lc_avgcost
//	lc_usamt[7]  = (lc_usqty[6] * lc_avgcost) - lc_usamt[6]  //이체차이금액=재공이체금액 - 자재금액
	lc_usamt[8]  = lc_usqty[8] * lc_avgcost
	lc_diffamt   = lc_sumusat - ( lc_usamt[1] + lc_usamt[2] + lc_usamt[3] + lc_usamt[4] + lc_usamt[5] &
											+ lc_usamt[6] + lc_usamt[7] + lc_usamt[8] )
	if lc_diffamt <> 0 then
		//차이금액 배분(2003.02.13 수정 : 현재공수량이 0인경우 차이금액을 금액조정으로 반영)
		if lc_sumusqt = 0 then
			if lc_ohqty = 0 then
				lc_usamt[9] = lc_usamt[9] + lc_diffamt
			else
				lc_ohamt = lc_ohamt 
				lc_usamt[9] = lc_usamt[9] + lc_diffamt
			end if
		else
			ll_cnt = 1
			ll_chkcnt = 0
			lc_remamt = lc_diffamt
			do while( ll_cnt <= 8 )
				if lc_usqty[ll_cnt] > 0 then
					ll_chkcnt = ll_cnt
					lc_usamt[ll_cnt]  = lc_usamt[ll_cnt] + truncate(((lc_usqty[ll_cnt] / lc_sumusqt) * lc_diffamt),0)
					lc_remamt = lc_remamt - truncate(((lc_usqty[ll_cnt] / lc_sumusqt) * lc_diffamt),0)
				end if
				ll_cnt = ll_cnt + 1
			loop
			if lc_remamt <> 0 then
				if ll_chkcnt = 0 then
					lc_ohamt = lc_ohamt + lc_remamt
				else
					lc_usamt[ll_chkcnt] = lc_usamt[ll_chkcnt] + lc_remamt
				end if
			end if
		end if
	end if
	
	for ll_cnt = 1 to 9										//내역별 사용금액 반영
		a_ds.setitem(a_curcnt, "wip003_wcusat" + string(ll_cnt),lc_usamt[ll_cnt])
	next
	
	ls_year = mid(a_postdate,1,4)
	ls_month = mid(a_postdate,5,2)
	
	//창고재공금액 UPDATE
	UPDATE "PBWIP"."WIP003"  
		SET "WCAVRG1" = :lc_avgcost, "WCBGAT1" = :lc_ohamt  
		WHERE ( "PBWIP"."WIP003"."WCCMCD" = :ls_cmcd ) AND  
				( "PBWIP"."WIP003"."WCPLANT" = :ls_plant ) AND  
				( "PBWIP"."WIP003"."WCDVSN" = :ls_dvsn ) AND  
				( "PBWIP"."WIP003"."WCITNO" = :ls_itno ) AND  
				( "PBWIP"."WIP003"."WCYEAR"||"PBWIP"."WIP003"."WCMONTH" = :a_postdate )  using sqlca;
	if sqlca.sqlcode = 0 and sqlca.SQLNRows > 0 then
		return 0
	else
	  //창고재공금액 이월 CREATION
	   INSERT INTO "PBWIP"."WIP003"  
         ( "WCCMCD","WCPLANT","WCDVSN","WCITNO","WCYEAR","WCMONTH","WCITCL","WCSRCE","WCPDCD",   
           "WCAVRG1","WCAVRG2","WCBGAT1","WCBGAT2","WCINQT","WCINAT1","WCINAT2","WCUSQT1","WCUSAT1",   
           "WCUSQT2","WCUSAT2","WCUSQT3","WCUSAT3","WCUSQT4","WCUSAT4","WCUSQT5","WCUSAT5","WCUSQT6",
			  "WCUSAT6","WCUSAT7","WCUSQT8","WCUSAT8","WCUSAT9","WCIPADDR","WCMACADDR","WCINPTDT","WCUPDTDT" )  
		VALUES ( :ls_cmcd,:ls_plant,:ls_dvsn,:ls_itno,:ls_year,:ls_month,:ls_itcl,:ls_srce,'',
				   :lc_avgcost,        0,:lc_ohamt,        0,       0,       0,        0,        0,        0,
						 0,        0,        0,        0,       0,       0,        0,        0,        0,
						 0,        0,        0,        0,       0,' ',' ',:g_s_date,' ' )  using sqlca;
		
		if sqlca.sqlcode = 0 then
			return 0
		else
			f_errorlog_insert(sqlca.sqlcode, sqlca.sqlerrtext)
			return -1
		end if
	end if	
end if

end function

