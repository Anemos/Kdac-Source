$PBExportHeader$f_pur040_get_mailaddr.srf
$PBExportComments$메일주소가져오기(본인-1,팀장-2)
global type f_pur040_get_mailaddr from function_object
end type

forward prototypes
global function string f_pur040_get_mailaddr (string as_empno, string as_gubun)
end prototypes

global function string f_pur040_get_mailaddr (string as_empno, string as_gubun);//////////////////////////////////////////
// 메일주소... :2008.09.12 완전보완
//
//		argument : as_empno   - 사번('1') 
//               as_gubun   - '1'  : 입력자
//                            '2'  : 입력자의 팀장
////////////////////////////////////////////////////
String	MailAddress, ls_empno
as_empno = Trim(as_empno)

IF as_gubun = '1' Then	
	SELECT coalesce(max(PEMAIL),'') INTO :MailAddress
	FROM PBCOMMON.DAC003
	WHERE PEEMPNO = :as_empno Using sqlca;
	
   //2004.11.11 추가
	MailAddress = Trim(MailAddress)
	
	If Len(MailAddress) < 1 Then 	
		MessageBox("확인",'메일주소:' + MailAddress + " 이메일 주소가 잘못되었습니다.")		
		Return "" 
	End If

Else	
	as_empno = Left(g_s_deptcd,2) + "00"	//부서코드
	
	SELECT  coalesce(max(dempno),'') INTO :ls_empno
   FROM   PBCOMMON.DAC001   
   where  DUSE  = ''    AND 
          DTODT  =  0    AND  
          DSYS1  = 'X'   AND 
          DCODE  = :as_empno Using sqlca ; 
			 
   If SQLCA.SQLCode <> 0 or trim(ls_empno) = '' Then 
		MessageBox("확인",'부서:' +  as_empno + " 해당부서의 팀장사번 조회오류! 연락바랍니다.")
		Return "" 
	End If
 
	SELECT coalesce(max(PEMAIL),'') INTO :MailAddress
	FROM PBCOMMON.DAC003
	WHERE PEJIKCHEK in ('3','4')
	  AND PEOUT <> '*'
  	  AND PEempno = :ls_empno Using sqlca ;
	
	//2004.11.11 추가
	MailAddress = Trim(MailAddress)
	If Len(MailAddress) < 1 Then 
		MessageBox("확인",'메일주소:' +  MailAddress + " 팀장 이메일 주소가 잘못되었습니다.")
		Return "" 
	End If
	
End IF	

//2004.11.11 추가  --> 2006.04.04 보완
String ls_mail
Long ln_pos
ln_pos = Pos(MailAddress, "@")

IF ln_pos = 0 Then	
	//MailAddress += "@kdac.co.kr"
	MessageBox("메일주소오류"  ,"메일주소가 잘못되었습니다.(@kdac.co.kr or @kdac.kr Missing) 메일주소 : " + MailAddress)
	Return ""
	
ElseIF ln_pos = 1 Then	//@kdac.co.kr만 등록된 경우.
	//MailAddress += "@kdac.co.kr"
	MessageBox("메일주소오류"  ,"메일주소가 잘못되었습니다. 메일주소 : " + MailAddress)
	Return ""
		
Else	
	ls_mail = Mid(MailAddress, ln_pos , Len(MailAddress) - (ln_pos - 1) )
		
	IF (Upper(ls_mail) = "@KDAC.CO.KR") OR (Upper(ls_mail) = "@KDAC.KR") THEN		
	Else	
		MessageBox("메일주소오류","메일주소가 잘못되었습니다.(외부메일주소 사용!) 메일주소 : " + MailAddress)
		Return ""		
	END IF
End IF

Return MailAddress

end function

