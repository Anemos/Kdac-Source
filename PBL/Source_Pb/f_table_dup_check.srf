$PBExportHeader$f_table_dup_check.srf
$PBExportComments$테이블에 중복된 데이타 체크
global type f_table_dup_check from function_object
end type

forward prototypes
global function boolean f_table_dup_check (datawindow adw, integer ai_row, string as_key[], string as_table)
end prototypes

global function boolean f_table_dup_check (datawindow adw, integer ai_row, string as_key[], string as_table);///////////////////////////////////////////////////////////////////////////////
//                                                                           
// Description :  테이블에 이미 값이 있는지를 검사한다.
//                                                                           
// return  :  boolean    false - ...  에러 발생시 메세지 뿌림 
//                       true                                                    
// Usage   :  f_table_dup_check(dw_1,1,"code","item_master")
//                                                                           
///////////////////////////////////////////////////////////////////////////////
string ls_orgin_select,ls_data,ls_sql,ls_where,ls_arg[],ls_col_type
int ii,li_col_num
long ll_cnt
dwitemstatus fg


fg = adw.GetItemStatus(ai_row, 0, Primary!) 
If fg <> NewModified!  Then
	return true
End if

li_col_num = UpperBound(as_key[])
If li_col_num < 1 Then return true  //체크할 Key가 없을때 

//조건절 생성
For ii = 1 To li_col_num
	ls_col_type = adw.Describe(as_key[ii]+".ColType")
	if left(ls_col_type,4) = "char" then 
		ls_arg[ii] 	= adw.GetItemString(ai_row,as_key[ii])
		ls_data 		= Upper(as_key[ii]) + ' = ' + "'" + ls_arg[ii] + "'"
	else
		ls_arg[ii]  = string(adw.GetItemNumber(ai_row,as_key[ii]))
		ls_data 		= 'TO_CHAR(' + Upper(as_key[ii]) + ') = ' + "'" + ls_arg[ii] + "'"
	end if
	
	If ii < li_col_num Then
		ls_where = ls_where + ls_data + ' AND '
	Else
		ls_where = ls_where + ls_data 
	End if
Next
//sql문장 생성
ls_where = ' WHERE ' + ls_where
ls_sql = 'SELECT COUNT(*) FROM ' + '"' + as_table + '"' +  ls_where
// Set up select statement using passed parameters
PREPARE sqlsa FROM :ls_sql;

DECLARE dyn_cursor DYNAMIC CURSOR FOR sqlsa;

OPEN DYNAMIC dyn_cursor;  

Fetch dyn_cursor into :ll_cnt;
if ll_cnt > 0 then
	MessageBox("확인","이미 존재하는 자료입니다.")
	adw.SetFocus()
	adw.Scrolltorow(ai_row)
	adw.Setrow(ai_row)
	CLOSE dyn_cursor;
	return false
else
	CLOSE dyn_cursor;
	return true
End if
end function

